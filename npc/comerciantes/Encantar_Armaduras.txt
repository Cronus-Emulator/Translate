//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Versão:                                                     ||
// - 1.3a                                                        ||
//===============================================================||
// = Alterações:                                                 ||
// - Tradução [Spell Master] / [Revisões Cronus]                 ||
//===============================================================||
// = Descrição:                                                  ||
// - Adiciona +1/+2/+3 status aleatórios á armaduras             ||
//===============================================================//

prontera,165,60,2	script	Aprendiz de Artesão	1_F_MERCHANT_01,{
	disable_items;
	mes "[Aprendiz de Artesão]";
	if (Zeny >= 400000) {
		mes "Venho estudando maneiras de melhorar uma armadura para aumentar a sua real capacidade.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "Encantar é uma habilidade incrível que funde um poder misterioso em um espaço escondido da armadura.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "No entanto, você tem que ter em mente que, se houver duas armaduras do mesmo tipo em seu inventário.";
		mes "O encantamento será aplicado na ordem em que estão colocados no seu inventário.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "Nesse caso, o encantamento pode ser aplicado a um item que você não queira encantar.";
		mes "Então basta trazer ^5555ffuma Armadura^000000 que você deseja, podendo então encantar com segurança.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "Eu não sou responsável por aquilo que pode acontecer se você tiver mais de um equipamento do mesmo tipo em seu inventário.";
		next;
		switch(select("Armadura sem Slot.","Armadura com Slot.","Armadura de Alto Nível.","Talvez na próxima.")) {
			case 1:
			setarray .@items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
			.@j = 50;
			break;
			case 2:
			setarray .@items[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
			.@j = 55;
			.@k = 1;
			break;
			case 3:
			setarray .@items[0],2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
			.@j = 60;
			break;
			case 4:
			mes "[Aprendiz de Artesão]";
			mes "Por favor, volte quando você tiver algum interesse em encantar sua armadura.";
			close;
		}
		.@menu$ = "";
		for(.@i = 0; .@i<getarraysize(.@items); ++.@i)
			.@menu$ += getitemname(.@items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@items[select(.@menu$)-1], .@j;
		end;
	}
	mes "Estou encarregado de encantar armaduras.";
	mes "Basta pôr, estive estudando formas de aumentar o poder das armaduras.";
	next;
	mes "[Aprendiz de Artesão]";
	mes "Se por acaso, você quiser encantar a sua armadura, traga-me 400,000 zeny e a armadura que pretende encantar e então tudo estará pronto.";
	close;

	S_EnchantArmor:
	.@itemid = getarg(0);
	.@failrate = getarg(1);
	mes "[Aprendiz de Artesão]";
	if (countitem(.@itemid) == 1) {
		mes "Adicionar um slot irá lhe custar 400,000 zeny.";
		mes "E haverá uma opção aleatória encantamento. Naturalmente, há uma chance de quebrar sua armadura.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "Primeiro e mais importante.";
		mes "^ff5555Refinamento existente ou carta na armadura serão perdidos.^000000";
		mes "Você ainda quer tentar encantar?";
		next;
		if(select("Deixe-me pensar mais.","Vá em frente.") == 1) {
			mes "[Aprendiz de Artesão]";
			mes "Bem, eu não posso culpá-lo. Segurança em primeiro lugar, certo?";
			mes "Agora tenha um bom dia.";
			close;
		}
		mes "[Aprendiz de Artesão]";
		mes "Então, um aventureiro hein? Bom, vamos.";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 400000) {
			mes "[Aprendiz de Artesão]";
			mes "Desculpe, mas você não tem zeny suficiente.";
			close;
		}
		progressbar "ffff00",7;
		Zeny -= 400000;
		delitem .@itemid,1;
		switch (rand(1,.@failrate)) {
			case 1: .@addpart = 4702;break;
			case 2: .@addpart = 4712;break;
			case 3: .@addpart = 4722;break;
			case 4: .@addpart = 4732;break;
			case 5: .@addpart = 4742;break;
			case 6: .@addpart = 4752;break;
			case 7:
			case 8: .@addpart = 4701;break;
			case 9:
			case 10: .@addpart = 4711;break;
			case 11:
			case 12: .@addpart = 4721;break;
			case 13:
			case 14: .@addpart = 4731;break;
			case 15:
			case 16: .@addpart = 4741;break;
			case 17:
			case 18: .@addpart = 4751;break;
			case 19:
			case 20:
			case 21: .@addpart = 4700;break;
			case 22:
			case 23:
			case 24: .@addpart = 4710;break;
			case 25:
			case 26:
			case 27: .@addpart = 4720;break;
			case 28:
			case 29:
			case 30: .@addpart = 4730;break;
			case 31:
			case 32:
			case 33: .@addpart = 4740;break;
			case 34:
			case 35:
			case 36: .@addpart = 4750;break;
			default:
			specialeffect2 EF_PHARMACY_FAIL;
			mes "[Aprendiz de Artesão]";
			mes "Bem, isso é muito ruim.";
			mes "Não consegui encantar seu equipamento.";
			close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "Hmm? Não há nada para se encantar!";
		mes "Por favor, volte com apenas UM equipamento para encantar.";
		close;
	}
}