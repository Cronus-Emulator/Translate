//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Versão:                                                     ||
// - 2.2                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Tradução [Spell Master]                                     ||
//===============================================================||
// = Descrição:                                                  ||
// - Aluga peco peco para cavaleiros e templários.               ||
// - Aluga falcão para caçadores.                                ||
//===============================================================//

//== PecoPeco Breeder (for Knights) ========================
prontera,55,350,5	script	Criador de Pecopecos#knt	8W_SOLDIER,{
	if(Upper == 0) { .@price = 2500; } //Normal Peco - default price
	if(Upper == 1) { .@price = 2500; } //Armored Peco
	if(Upper == 2) { .@price = 2500; } //Baby Peco
	mes "[Criador de Pecopecos]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "Bom-dia honorável Cavaleiro gostaria de alugar um Pecopeco?";
		mes "Você precisa ter a Habilidade Montaria e apenas "+.@price+" Zeny sobrando.";
		next;
		switch(select("Alugar Peco Peco","Cancelar")) {
			case 1:
			if (Zeny < .@price) {
				mes "[Criador de Pecopecos]";
				mes "Sinto muito mas você não tem zenys o suficiente.";
				close;
			}
			else if(getskilllv(KN_RIDING) == 0) {
				mes "[Criador de Pecopecos]";
				mes "Você não pode usar meus serviços até aprender a habilidade Montaria.";
				close;
			}
			else if(checkmount()) {
				mes "[Criador de Pecopecos]";
				mes "Mas você já está montado em um Pecopeco.";
				close;
			}
			else if(hascashmount()) {
				mes "[Criador de Pecopecos]";
				mes "Por favor remova sua montaria atual.";
				close;
			}
			Zeny -= .@price;
			setmount(MOUNT_PECO);
			close;
			case 2:
			mes "[Criador de Pecopecos]";
			mes "Bem então, tenha um dia bom.";
			close;
		}
	}
	else {
		mes "Me desculpe mas o aluguel de pecopecos só está disponível para cavaleiros e lordes.";
		close;
	}
}

//== Grand PecoPeco Breeder (for Crusaders) ================
prontera,232,318,3	script	Criador de Pecopecos#cru	8W_SOLDIER,{
	if(Upper==0) .@price = 3500; //Normal Peco - default price
	if(Upper==1) .@price = 3500; //Armored Peco
	if(Upper==2) .@price = 3500; //Baby Peco

	mes "[PecoPeco Breeder]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) {
			mes "Bem vindo, Templário.";
		}
		else {
			mes "Bem vindo, Paladino.";
		}
		mes "Nós possuímos um Pecopeco especial preparado para você.";
		mes "Para adquirir um Pecopeco são necessários "+.@price+" Zeny.";
		next;
		switch(select("Alugar a PecoPeco","Sair")) {
			case 1:
			if (Zeny < .@price) {
				mes "[Criador de Pecopecos]";
				mes "Você não possui a quantidade de zenys suficientes.";
				mes "Se você quiser um Pecopeco por favor me traga "+.@price+" zenys...";
				close;
			}
			else if(getskilllv(KN_RIDING) == 0) {
				mes "[Criador de Pecopecos]";
				mes "Você deve primeiro aprender a como montar um Pecopeco após isso eu poderei alugar um para você.";
				close;
			}
			else if(checkmount()) {
				mes "[Criador de Pecopecos]";
				mes "Você já está montado em uma montaria";
				close;
			}
			else if(hascashmount()) {
				mes "[Criador de Pecopecos]";
				mes "Por favor remova sua montaria atual.";
				close;
			}
			Zeny -= .@price;
			setmount(MOUNT_PECO);
			close;
			case 2:
			mes "[PecoPeco Breeder]";
			mes "Nos vemos por ai.";
			close;
		}
	}
	else {
		mes "O que posso fazer por você?";
		mes "Lembre-se que esse serviço é restrito a Templários e Paladinos.";
		close;
	}
}

//== Falcon Master =========================================
hu_in01,381,304,5	script	Adestrador de Falcões#hnt	8W_SOLDIER,{
	if(Upper == 0) .@price = 2500; //Normal Falcon - default price
	if(Upper == 1) .@price = 2500; //Scarf Falcon
	if(Upper == 2) .@price = 2500; //Baby Falcon

	mes "[Adestrador de Falcões]";
	if (BaseJob == Job_Hunter) {
		if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
			mes "Hum...";
			mes "Você não pode adquirir um Falcão enquanto não remover seu lobo!";
			close;
		}
		mes "Você precisa de um Falcão?";
		mes "Eles custam apenas "+.@price+" zenys!";
		next;
		switch(select("Alugar Falcon","Cancelar")) {
			case 1:
			if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
				mes "[Adestrador de Falcões]";
				mes "Hum...";
				mes "Você não pode adquirir um Falcão enquanto não remover seu lobo!";
				close;
			}
			if (Zeny < .@price) {
				mes "[Adestrador de Falcões]";
				mes "O quê?";
				mes "Você não possui a quantidade de zenys suficiente?!";
				mes "Primeiro você deve juntar dinheiro em vez de caçar monstros.";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Adestrador de Falcões]";
				mes "Poxa";
				mes "Primeiro vá e aprenda a domesticar um Falcão!";
				mes "Eu não posso te entregar um falcão sendo que você não saberá cuidar dele.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Adestrador de Falcões]";
				mes "Hum...";
				mes "Você já possui um Falcão.";
				mes "Você não consegue vê-lo?";
				close;
			}
			Zeny -= .@price;
			setfalcon;
			close;
			case 2:
			mes "[Adestrador de Falcões]";
			mes "E-espere, onde você vai?";
			mes "Estes Falcões são de elite, eu garanto!";
			mes "Vamos lá.";
			close;
		}
	}
	else {
		mes "Falcões podem apenas serem usados por Caçadores e Atiradores de Elite, aprendeu?";
		mes "...Heheh, ciúmes?";
		close;
	}
}