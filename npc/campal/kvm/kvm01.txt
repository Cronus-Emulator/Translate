//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Nome do NPC:                                                ||
// - kvm01.txt                                                   ||
//===============================================================||
// = Criação:                                                    ||
// - eA/Herc/Cronus                                              ||
//===============================================================||
// = Versão:                                                     ||
// - 1.0                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Cabeçalho [SlexFire]                                        ||
// - Tradução [Revisões Cronus] / [SlexFire]                     ||
//===============================================================||
// = Descrição:                                                  ||
// - Batalha Campal KvM (variação: 80 - 99)                      ||
//===============================================================||
// = Observações:                                                ||
// - Equipe vencedora - 5 emblemas                               ||
// - Equipe perdedora - 1 emblema                                ||
//===============================================================//

// Sala de espera
//============================================================
bat_room,169,226,5	script	Sala de espera KVM#a::KvM01R_Guillaume	4_M_KY_KNT,{
	end;

OnInit:
	waitingroom "Estação de Batalha 5 Jogadores",6,"KvM01_BG::OnGuillaumeJoin",1;
	end;

OnEnterBG:
	$@KvM01BG_id1 = waitingroom2bg("bat_c01",52,129,"KvM01_BG::OnGuillaumeQuit","KvM01_BG::OnGuillaumeDie");
	end;
}

bat_room,169,220,0	warp	#kvm801	2,2,bat_room,154,150

bat_room,169,205,3	script	Sala de espera KVM#b::KvM01R_Croix	4_M_CRU_KNT,{
	end;

OnInit:
	waitingroom "Estação de Batalha 5 Jogadores",6,"KvM01_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	$@KvM01BG_id2 = waitingroom2bg("bat_c01",147,55,"KvM01_BG::OnCroixQuit","KvM01_BG::OnCroixDie");
	end;
}

bat_room,169,211,0	warp	#kvm802	2,2,bat_room,154,150

// Partida
//============================================================
-	script	KVM01::CellEffect	-1,{
	end;

OnKvM01One:
	specialeffect EF_CHANGECOLD;
	end;

OnKvM01Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c01,54,124,3	duplicate(CellEffect)	#RedcellA1-1	HIDDEN_WARP_NPC
bat_c01,55,124,3	duplicate(CellEffect)	#RedcellA2-1	HIDDEN_WARP_NPC
bat_c01,56,124,3	duplicate(CellEffect)	#RedcellA3-1	HIDDEN_WARP_NPC
bat_c01,57,124,3	duplicate(CellEffect)	#RedcellA4-1	HIDDEN_WARP_NPC
bat_c01,57,125,3	duplicate(CellEffect)	#RedcellA5-1	HIDDEN_WARP_NPC
bat_c01,57,126,3	duplicate(CellEffect)	#RedcellA6-1	HIDDEN_WARP_NPC
bat_c01,57,127,3	duplicate(CellEffect)	#RedcellA7-1	HIDDEN_WARP_NPC

bat_c01,145,59,3	duplicate(CellEffect)	#RedcellB1-1	HIDDEN_WARP_NPC
bat_c01,144,59,3	duplicate(CellEffect)	#RedcellB2-1	HIDDEN_WARP_NPC
bat_c01,143,59,3	duplicate(CellEffect)	#RedcellB3-1	HIDDEN_WARP_NPC
bat_c01,142,59,3	duplicate(CellEffect)	#RedcellB4-1	HIDDEN_WARP_NPC
bat_c01,142,56,3	duplicate(CellEffect)	#RedcellB5-1	HIDDEN_WARP_NPC
bat_c01,142,57,3	duplicate(CellEffect)	#RedcellB6-1	HIDDEN_WARP_NPC
bat_c01,142,58,3	duplicate(CellEffect)	#RedcellB7-1	HIDDEN_WARP_NPC

bat_c01,54,128,3	script	#A_camp_start01	HIDDEN_WARP_NPC,4,4,{
	end;

OnInit:
	disablenpc "#A_camp_start01";
	end;

OnEnable:
	enablenpc "#A_camp_start01";
	end;

OnDisable:
	disablenpc "#A_camp_start01";
	end;

OnTouch:
	Bat_Team = 1;
	setquest 6025;
	end;
}

bat_c01,146,56,3	script	#B_camp_start01	HIDDEN_WARP_NPC,4,4,{
	end;

OnInit:
	disablenpc "#B_camp_start01";
	end;

OnEnable:
	enablenpc "#B_camp_start01";
	end;

OnDisable:
	disablenpc "#B_camp_start01";
	end;

OnTouch:
	Bat_Team = 2;
	setquest 6025;
	end;
}

// Mecanismo da Battleground
//============================================================
-	script	KvM01_BG	-1,{
	end;

OnInit:
	setwall "bat_c01",54,122,6,7,0,"batc01wall_a";
	setwall "bat_c01",55,122,5,7,0,"batc01wall_b";
	setwall "bat_c01",140,56,6,7,0,"batc01wall_c";
	setwall "bat_c01",140,57,5,7,0,"batc01wall_d";
	disablenpc "Oficial KVM#KVM01A";
	disablenpc "Oficial KVM#KVM01B";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM01_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	//BG_Delay_Tick = gettimetick(2) + 1200;
OnGuillaumeDie:
	if ($@KvM01BG == 2) {
		--.Guillaume_Count;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM01_BG::OnCroixWin";
		else {
			mapannounce "bat_c01", "O número de membros de Guillaume é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c01", "O número de membros de Croix é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//BG_Delay_Tick = gettimetick(2) + 1200;
OnCroixDie:
	if ($@KvM01BG == 2) {
		--.Croix_Count;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM01_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c01", "O número de membros de Guillaume é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c01", "O número de membros de Croix é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnReadyCheck:
	if( $@KvM01BG )
		end;
	.@Guillaume = getwaitingroomstate(0,"KvM01R_Guillaume");
	.@Croix = getwaitingroomstate(0,"KvM01R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	$@KvM01BG = 1; // Starting
	donpcevent "KvM01R_Croix::OnEnterBG";
	donpcevent "KvM01R_Guillaume::OnEnterBG";
	donpcevent "KvM01_BG::OnStart";
	end;

OnStart:
	disablenpc "Oficial KVM#KVM01A";
	disablenpc "Oficial KVM#KVM01B";
	$@KvM01BG_Victory = 0;
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Em 1 minuto, o KVM irá começar.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "O tempo máximo de uma batalha KVM são de 5 minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c01", "Se preparem para a batalha KVM.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Vocês podem usar buffs em seus times.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start01::OnEnable";
	donpcevent "#B_camp_start01::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start01::OnDisable";
	donpcevent "#B_camp_start01::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c01", "30 segundos restantes para iniciar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c01", "15 segundos restantes para iniciar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c01", "10 segundos restantes para iniciar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c01", "5 segundos restantes para iniciar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c01", "KVM está começando agora.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	.Guillaume_Count = bg_get_data($@KvM01BG_id1, 0);
	.Croix_Count = bg_get_data($@KvM01BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		$@KvM01BG_Victory = 3;
		$@KvM01BG = 3;
		mapannounce "bat_c01","Não há jogadores suficientes para começar a batalha.",1,0x696969;
		stopnpctimer;
		donpcevent "KvM01_BG::OnStop";
		end;
	}
	$@KvM01BG = 2; // Playing
	bg_warp $@KvM01BG_id1,"bat_c01",61,120;
	bg_warp $@KvM01BG_id2,"bat_c01",138,63;
	end;

OnTimer300000:
	mapannounce "bat_c01", "1 minuto restante para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c01", "30 segundos restantes para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c01", "15 segundos restantes para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c01", "10 segundos restantes para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c01", "5 segundos restantes para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c01", "A batalha KVM acabou.",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM01_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM01_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		$@KvM01BG = 3;
		$@KvM01BG_Victory = 3;
		mapannounce "bat_c01", "O número de membros de Guillaume é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "O número de membros de Croix é "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "A batalha terminou em empate.",bc_map,"0x00ff00";
		donpcevent "KvM01_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	$@KvM01BG = 3;
	$@KvM01BG_Victory = 1;
	mapannounce "bat_c01", "Guillaume venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Parabéns aos membros de Guillaume.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Todos serão enviados ao ponto inicial.",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnCroixWin:
	$@KvM01BG = 3;
	$@KvM01BG_Victory = 2;
	mapannounce "bat_c01", "Croix venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Parabéns aos membros de Croix.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Todos serão enviados ao ponto inicial.",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "Oficial KVM#KVM01A";
	enablenpc "Oficial KVM#KVM01B";
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	donpcevent "KvM01_BG_Out::OnBegin";
	end;

OnReset:
	.Croix_Count = 0;
	.Guillaume_Count = 0;
	$@KvM01BG_Victory = 0;
	if( $@KvM01BG_id1 ) { bg_destroy $@KvM01BG_id1; $@KvM01BG_id1 = 0; }
	if( $@KvM01BG_id2 ) { bg_destroy $@KvM01BG_id2; $@KvM01BG_id2 = 0; }
	disablenpc "Oficial KVM#KVM01A";
	disablenpc "Oficial KVM#KVM01B";
	mapwarp "bat_c01","bat_room",154,150;
	maprespawnguildid "bat_c01",0,3; // Just in case someone else
	bg_updatescore "bat_c01",5,5;
	$@KvM01BG = 0;
	donpcevent "KvM01_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM01_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Por favor fale com o Oficial para receber os pontos KVM.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "O Oficial dará a você os pontos por 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Em 30 segundos, o Oficial deixará o local.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c01", "Se você não falar com o Oficial, você não irá receber os pontos.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Por favor tenha cuidado.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_c01", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	donpcevent "KvM01_BG::OnReset";
	end;
}

// Recompensas Battleground
//============================================================
bat_c01,51,130,5	script	Oficial KVM#KVM01A	4_M_KY_HEAD,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == Bat_Team )
		{ // Victory
			kvm_point += 5;
			mes "[Oficial KVM]";
			mes "Bom jogo.";
			mes "Que a glória do KVM esteja com você.";
			mes "Você recebe os pontos de vencedor: 5";
			close2;
		} else {
			++kvm_point;
			mes "[Oficial KVM]";
			mes "Sinto muito.";
			mes "Espero que tenha boa sorte na próxima vez.";
			mes "Você recebe os pontos de derrota: 1";
			close2;
		}
		bg_leave;
		Bat_Team = 0;
		warp "bat_room",154,150;
		end;
	}
	end;
}

bat_c01,148,53,1	script	Oficial KVM#KVM01B	4_M_CRU_HEAD,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == Bat_Team )
		{ // Victory
			kvm_point += 5;
			mes "[Oficial KVM]";
			mes "Bom jogo.";
			mes "Que a glória do KVM esteja com você.";
			mes "Você recebe os pontos de vencedor: 5";
			close2;
		} else {
			++kvm_point;
			mes "[Oficial KVM]";
			mes "Sinto muito.";
			mes "Espero que tenha boa sorte na próxima vez.";
			mes "Você recebe os pontos de derrota: 1";
			close2;
		}
		bg_leave;
		Bat_Team = 0;
		warp "bat_room",154,150;
		end;
	}
	end;
}
