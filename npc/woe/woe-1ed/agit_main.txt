//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Criação:                                                    ||
// - eA/Herc/Cronus                                              ||
//===============================================================||
// = Versão:                                                     ||
// - 2.0                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Cabeçalho [SlexFire], [Spell Master]                        ||
// - Tradução [Spell Master]                                     ||
// - Revisão em busca de erros [Spell Master]                    ||
//===============================================================||
// = Descrição:                                                  ||
// - Gerencia Scripts da GDE Primeira edição                     ||
//===============================================================||
// = Observações:                                                ||
// - Arquivo revisado, não foram encontrados erros               ||
//===============================================================//

//==============================================================
// WoE : Principais Funções
//==============================================================
-	script	Gld_Agit_Manager::Gld_Agit_Manager	-1,{
	end;

//--------------------------------------------------------------
// A guerra do emperium começou.
//--------------------------------------------------------------
OnAgitStart:
	if (strnpcinfo(0) == "Gld_Agit_Manager") end;
	maprespawnguildid strnpcinfo(2),getcastledata(strnpcinfo(2),1),6;
	gvgon strnpcinfo(2);

// Invoca o emperium mesmo quando ele é quebrado.
OnStartArena:
	if (strnpcinfo(2) == "aldeg_cas01") { setarray .@emproom[0],216,23; }
	else if (strnpcinfo(2) == "aldeg_cas02") { setarray .@emproom[0],213,23; }
	else if (strnpcinfo(2) == "aldeg_cas03") { setarray .@emproom[0],205,31; }
	else if (strnpcinfo(2) == "aldeg_cas04") { setarray .@emproom[0],36,217; }
	else if (strnpcinfo(2) == "aldeg_cas05") { setarray .@emproom[0],27,101; }
	else if (strnpcinfo(2) == "gefg_cas01") { setarray .@emproom[0],197,181; }
	else if (strnpcinfo(2) == "gefg_cas02") { setarray .@emproom[0],176,178; }
	else if (strnpcinfo(2) == "gefg_cas03") { setarray .@emproom[0],244,166; }
	else if (strnpcinfo(2) == "gefg_cas04") { setarray .@emproom[0],174,177; }
	else if (strnpcinfo(2) == "gefg_cas05") { setarray .@emproom[0],194,184; }
	else if (strnpcinfo(2) == "payg_cas01") { setarray .@emproom[0],139,139; }
	else if (strnpcinfo(2) == "payg_cas02") { setarray .@emproom[0],38,25; }
	else if (strnpcinfo(2) == "payg_cas03") { setarray .@emproom[0],269,265; }
	else if (strnpcinfo(2) == "payg_cas04") { setarray .@emproom[0],270,28; }
	else if (strnpcinfo(2) == "payg_cas05") { setarray .@emproom[0],30,30; }
	else if (strnpcinfo(2) == "prtg_cas01") { setarray .@emproom[0],197,197; }
	else if (strnpcinfo(2) == "prtg_cas02") { setarray .@emproom[0],157,174; }
	else if (strnpcinfo(2) == "prtg_cas03") { setarray .@emproom[0],16,220; }
	else if (strnpcinfo(2) == "prtg_cas04") { setarray .@emproom[0],291,14; }
	else if (strnpcinfo(2) == "prtg_cas05") { setarray .@emproom[0],266,266; }
	// Adicione seus castelos custominizados a partir daqui.
	else {
		end;
	}
	if (!mobcount(strnpcinfo(2),"Agit#"+strnpcinfo(2)+"::OnAgitBreak")) {
		monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Emperium",1288,1,"Agit#"+strnpcinfo(2)+"::OnAgitBreak";
	}
	end;

//--------------------------------------------------------------
// O emperium foi quebrado.
//--------------------------------------------------------------
OnAgitBreak:
	.@GID = getcharid(2);
	// Mostra um Log de erro caso algum personagem sem clã quebre o emperium, mesmo que isso é impossível de acontecer
	if (.@GID <= 0) {
		.@notice$ = "O personagem "+strcharinfo(0)+" ("+getcharid(0)+") acaba de quebrar o Emperium no Castelo: "+strnpcinfo(2)+" sem estar em um Clã. O Emperium será restaurado";
		logmes .@notice$; debugmes .@notice$;
		donpcevent "Agit#"+strnpcinfo(2)+"::OnStartArena";
		end;
	}
	// Ajusta a economia do castelo
	.@Economy = getcastledata(strnpcinfo(2),2) - 5;
	if (.@Economy < 0) .@Economy = 0;
	setcastledata strnpcinfo(2), 2, .@Economy;
	// Ajusta o nível de defesa do castelo
	.@defence = getcastledata(strnpcinfo(2),3) - 5;
	if (.@defence < 0) .@defence = 0;
	setcastledata strnpcinfo(2), 3, .@defence;

	// Define o novo ocupante para o castelo
	setcastledata strnpcinfo(2),1, .@GID;

	// Anuncia o novo ocupante para o castelo.
	mapannounce strnpcinfo(2),"O Emperium foi destruído.",bc_map|bc_woe,"0x00CCFF",FW_NORMAL,12;
	maprespawnguildid strnpcinfo(2),.@GID,6;

	// Atualiza dados do castelo como investimentos, kafra etc...
	donpcevent strnpcinfo(0)+"::OnRecvCastle";
	disablenpc "Funcionária Kafra#"+strnpcinfo(2);
	for (.@i = 4; .@i <= 9; ++.@i) {
		setcastledata strnpcinfo(2), .@i, 0;
	}
	// Apaga dados de invocação dos guardões.
	if( getgdskilllv(.@GID,10002) == 0 ) {
		for (.@i = 10; .@i <= 17; ++.@i) {
			setcastledata strnpcinfo(2), .@i, 0;
		}
	}
	// Re-Adiciona o Emperium.
	sleep 500; // Pausa o script por 5 segundos
	if( agitcheck() )
		donpcevent "Agit#"+strnpcinfo(2)+"::OnStartArena";
	sleep 7000;
	announce "O castelo de ["+getcastlename(strnpcinfo(2))+"] foi conquistado pelo Clã ["+getguildname(.@GID)+"].",bc_all|bc_woe;
	end;

//--------------------------------------------------------------
// A guerra do emperium terminou.
//--------------------------------------------------------------
OnAgitEnd:
	if (strnpcinfo(0) == "Gld_Agit_Manager") end;
	gvgoff strnpcinfo(2);
	// Não elimina o emperium caso o castelo não tenha nenhum ocupante.
	if (getcastledata(strnpcinfo(2),1)) {
		killmonster strnpcinfo(2),"Agit#"+strnpcinfo(2)+"::OnAgitBreak";
	}
	end;

//--------------------------------------------------------------
// O castelo foi abandonado.
//--------------------------------------------------------------
OnGuildBreak:
	if (strnpcinfo(0) == "Gld_Agit_Manager") end;
	// Elimina os Guardiões e Kafra.
	killmonster strnpcinfo(2),"Guardião#"+strnpcinfo(2)+"::OnGuardianDied";
	disablenpc "Funcionária Kafra#"+strnpcinfo(2);
	setcastledata strnpcinfo(2),1,0;
	// Espera um estante para anunciar.
	sleep 7000;
	announce "O castelo [" + getcastlename(strnpcinfo(2)) + "] foi abandonado.",0;
	donpcevent strnpcinfo(0)+"::OnRecvCastle";
	end;

OnAgitInit:
OnRecvCastle:
	if (strnpcinfo(0) == "Gld_Agit_Manager") end;
	// Invoca monstros quando o castelo está vazio.
	.@GID = getcastledata(strnpcinfo(2),1);
	if (.@GID == 0) {
		killmonsterall strnpcinfo(2);
		if (compare(strnpcinfo(2),"aldeg")) {
			// Invocação normal
			monster strnpcinfo(2),0,0,"Druidra Malígno",1117,10;
			monster strnpcinfo(2),0,0,"Khalitzburg",1132,4;
			monster strnpcinfo(2),0,0,"Cavaleiro do Abismo",1219,2;
			monster strnpcinfo(2),0,0,"Executioner",1205,1;
			monster strnpcinfo(2),0,0,"Penomena",1216,10;
			monster strnpcinfo(2),0,0,"Alarme",1193,18;
			monster strnpcinfo(2),0,0,"Relôgio",1269,9;
			monster strnpcinfo(2),0,0,"Raydric Arqueiro",1276,7;
			monster strnpcinfo(2),0,0,"Andarilho",1208,3;
			monster strnpcinfo(2),0,0,"Alice",1275,1;
			monster strnpcinfo(2),0,0,"Cavaleiro Sanguinário",1268,1;
			monster strnpcinfo(2),0,0,"Senhor das Trevas",1272,1;
			// Define as coordenadas na sala do emperium para invocar os monstros.
			if (strnpcinfo(2) == "aldeg_cas01") { setarray .@emproom[0],216,23; }
			else if (strnpcinfo(2) == "aldeg_cas02") { setarray .@emproom[0],213,23; }
			else if (strnpcinfo(2) == "aldeg_cas03") { setarray .@emproom[0],205,31; }
			else if (strnpcinfo(2) == "aldeg_cas04") { setarray .@emproom[0],36,217; }
			else if (strnpcinfo(2) == "aldeg_cas05") { setarray .@emproom[0],27,101; }
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Senhor das Trevas",1272,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Gerente",1270,4;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Cavaleiro Sanguinário",1268,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Cavaleiro do Abismo",1219,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Raydric Arqueiro",1276,5;
		}
		else if (compare(strnpcinfo(2),"gefg")) {
			// Invocação normal
			monster strnpcinfo(2),0,0,"Druidra Malígno",1117,10;
			monster strnpcinfo(2),0,0,"Xamã do Vento",1263,11;
			monster strnpcinfo(2),0,0,"Bathory",1102,10;
			monster strnpcinfo(2),0,0,"Jack",1130,10;
			monster strnpcinfo(2),0,0,"Marduk",1140,20;
			monster strnpcinfo(2),0,0,"Raydric",1163,9;
			monster strnpcinfo(2),0,0,"Alice",1275,1;
			monster strnpcinfo(2),0,0,"Cavaleiro do Abismo",1219,1;
			monster strnpcinfo(2),0,0,"Flor do Luar",1150,1;
			monster strnpcinfo(2),0,0,"Freeoni",1159,1;
			// Define as coordenadas na sala do emperium para invocar os monstros.
			if (strnpcinfo(2) == "gefg_cas01") { setarray .@emproom[0],197,181; }
			else if (strnpcinfo(2) == "gefg_cas02") { setarray .@emproom[0],176,178; }
			else if (strnpcinfo(2) == "gefg_cas03") { setarray .@emproom[0],244,166; }
			else if (strnpcinfo(2) == "gefg_cas04") { setarray .@emproom[0],174,177; }
			else if (strnpcinfo(2) == "gefg_cas05") { setarray .@emproom[0],194,184; }
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Mysteltainn",1203,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Orc Herói",1087,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Grand Orc",1213,10;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Orc Arqueiro",1189,10;
		}
		else if (compare(strnpcinfo(2),"payg")) {
			// Invocação normal
			monster strnpcinfo(2),0,0,"Guardião da Floresta",1277,9;
			monster strnpcinfo(2),0,0,"Andarilho",1208,10;
			monster strnpcinfo(2),0,0,"Dragão Mutante",1262,5;
			monster strnpcinfo(2),0,0,"Bathory",1102,5;
			monster strnpcinfo(2),0,0,"Flor do Luar",1150,1;
			monster strnpcinfo(2),0,0,"Eddga",1115,1;
			monster strnpcinfo(2),0,0,"Horong",1129,11;
			monster strnpcinfo(2),0,0,"Raydric Arqueiro",1276,5;
			monster strnpcinfo(2),0,0,"Kobold Arqueiro",1282,4;
			monster strnpcinfo(2),0,0,"Gárgula",1253,5;
			// Define as coordenadas na sala do emperium para invocar os monstros.
			if (strnpcinfo(2) == "payg_cas01") { setarray .@emproom[0],139,139; }
			else if (strnpcinfo(2) == "payg_cas02") { setarray .@emproom[0],38,25; }
			else if (strnpcinfo(2) == "payg_cas03") { setarray .@emproom[0],269,265; }
			else if (strnpcinfo(2) == "payg_cas04") { setarray .@emproom[0],270,28; }
			else if (strnpcinfo(2) == "payg_cas05") { setarray .@emproom[0],30,30; }
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Flor do Luar",1150,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Eddga",1115,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Andarilho",1208,6;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Raydric Arqueiro",1276,5;
		}
		else if (compare(strnpcinfo(2),"prtg")) {
			// Invocação normal
			monster strnpcinfo(2),0,0,"Raydric",1163,1;
			monster strnpcinfo(2),0,0,"Khalitzburg",1132,10;
			monster strnpcinfo(2),0,0,"Cavaleiro do Abismo",1219,5;
			monster strnpcinfo(2),0,0,"Cavaleiro Sanguinário",1268,5;
			monster strnpcinfo(2),0,0,"Cavaleiro da Tempestade",1251,1;
			monster strnpcinfo(2),0,0,"Hatii",1252,1;
			monster strnpcinfo(2),0,0,"Raydric Arqueiro",1276,5;
			monster strnpcinfo(2),0,0,"Grifo",1259,2;
			monster strnpcinfo(2),0,0,"Quiméra",1283,3;
			monster strnpcinfo(2),0,0,"Alice",1275,1;
			monster strnpcinfo(2),0,0,"Jirtas",1200,1;
			// Define as coordenadas na sala do emperium para invocar os monstros.
			if (strnpcinfo(2) == "prtg_cas01") { setarray .@emproom[0],197,197; }
			else if (strnpcinfo(2) == "prtg_cas02") { setarray .@emproom[0],157,174; }
			else if (strnpcinfo(2) == "prtg_cas03") { setarray .@emproom[0],16,220; }
			else if (strnpcinfo(2) == "prtg_cas04") { setarray .@emproom[0],291,14; }
			else if (strnpcinfo(2) == "prtg_cas05") { setarray .@emproom[0],266,266; }
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Cavaleiro Sanguinário",1268,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Cavaleiro da Tempestade",1251,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Hatii",1252,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Cavaleiro do Abismo",1219,1;
			monster strnpcinfo(2),.@emproom[0],.@emproom[1],"Raydric Arqueiro",1276,5;
		}
		// Adicione seus castelos custominizados a partir daqui.
		else {
			end;
		}
		donpcevent "Agit#"+strnpcinfo(2)+"::OnStartArena";
		// Desativa a Kafra
		disablenpc "Funcionária Kafra#"+strnpcinfo(2);
		end;
	}
	else {
		// Coloca os emblemas nas bandeiras.
		if (strnpcinfo(2) == "aldeg_cas01") { donpcevent "::OnRecvCastleA01"; }
		else if (strnpcinfo(2) == "aldeg_cas02") { donpcevent "::OnRecvCastleA02"; }
		else if (strnpcinfo(2) == "aldeg_cas03") { donpcevent "::OnRecvCastleA03"; }
		else if (strnpcinfo(2) == "aldeg_cas04") { donpcevent "::OnRecvCastleA04"; }
		else if (strnpcinfo(2) == "aldeg_cas05") { donpcevent "::OnRecvCastleA05"; }
		else if (strnpcinfo(2) == "gefg_cas01") { donpcevent "::OnRecvCastleG01"; }
		else if (strnpcinfo(2) == "gefg_cas02") { donpcevent "::OnRecvCastleG02"; }
		else if (strnpcinfo(2) == "gefg_cas03") { donpcevent "::OnRecvCastleG03"; }
		else if (strnpcinfo(2) == "gefg_cas04") { donpcevent "::OnRecvCastleG04"; }
		else if (strnpcinfo(2) == "gefg_cas05") { donpcevent "::OnRecvCastleG05"; }
		else if (strnpcinfo(2) == "payg_cas01") { donpcevent "::OnRecvCastlePy01"; }
		else if (strnpcinfo(2) == "payg_cas02") { donpcevent "::OnRecvCastlePy02"; }
		else if (strnpcinfo(2) == "payg_cas03") { donpcevent "::OnRecvCastlePy03"; }
		else if (strnpcinfo(2) == "payg_cas04") { donpcevent "::OnRecvCastlePy04"; }
		else if (strnpcinfo(2) == "payg_cas05") { donpcevent "::OnRecvCastlePy05"; }
		else if (strnpcinfo(2) == "prtg_cas01") { donpcevent "::OnRecvCastlePt01"; }
		else if (strnpcinfo(2) == "prtg_cas02") { donpcevent "::OnRecvCastlePt02"; }
		else if (strnpcinfo(2) == "prtg_cas03") { donpcevent "::OnRecvCastlePt03"; }
		else if (strnpcinfo(2) == "prtg_cas04") { donpcevent "::OnRecvCastlePt04"; }
		else if (strnpcinfo(2) == "prtg_cas05") { donpcevent "::OnRecvCastlePt05"; }
		// Adicione seus castelos custominizados a partir daqui.
		else {
			end;
		}
		// Carrega os Guardiões.
		donpcevent "Guardião#"+strnpcinfo(2)+"::OnSpawnGuardians";
		// Atualiza a Kafra
		if (getcastledata(strnpcinfo(2),9) < 1) disablenpc "Funcionária Kafra#"+strnpcinfo(2);
	}
	end;
}

//============================================================
// WoE : Controle sobre as Funcionárias Kafra
//============================================================
-	script	Funcionária Kafra#woe::guildkafra	-1,{

	.@GID = getcastledata(strnpcinfo(2),1);

	if (compare(strnpcinfo(2),"aldeg")) {
		setarray .@destination$[0],"Al De Baran","aldebaran";
		setarray .@coordinates[0],132,103;
	}
	else if (compare(strnpcinfo(2),"gefg")) {
		setarray .@destination$[0],"Geffen","geffen";
		setarray .@coordinates[0],120,39;
	}
	else if (compare(strnpcinfo(2),"payg")) {
		setarray .@destination$[0],"Payon","payon";
		setarray .@coordinates[0],70,100;
	}
	else if (compare(strnpcinfo(2),"prtg")) {
		setarray .@destination$[0],"Prontera","prontera";
		setarray .@coordinates[0],278,211;
	}
	// Adicione seus castelos custominizados a partir daqui.
	else {
		end;
	}

	cutin "kafra_01",2;
	if (getcharid(2) == .@GID) {
		mes "[Funcionária Kafra]";
		mes "Bem vind"+(Sex?"o":"a")+".";
		mes "Membro ^ff0000"+getguildname(.@GID)+"^000000.";
		mes "A Corporação Kafra estará com você aonde quer que vá.";
		next;
		switch (select("Usar Armazém:Usar Serviço de Teleporte:Alugar Carrinho:Cancelar")) {
		case 1:
			if (getskilllv("NV_BASIC") < 6) {
				mes "[Funcionária Kafra]";
				mes "Sinto muito, mas você não tem a Habilidades";
				mes "Básicas Nível 6 de Aprendiz para usar o Serviço de Armazém.";
				close2;
				cutin "",255;
				end;
			}
			mes "[Funcionária Kafra]";
			mes "Deixe-me abrir o Armazém para você.";
			mes "Obrigado por utilizar o Serviço da corporação Kafra.";
			close2;
			cutin "",255;
			openstorage;
			end;
		case 2:
			mes "[Funcionária Kafra]";
			mes "Por favor selecione seu destino";
			next;
			switch (select(.@destination$+" -> 200z:Cancelar")) {
			case 1:
				if (Zeny < 200) {
					mes "[Funcionária Kafra]";
					mes "Sinto muito, mas você não tem Zeny suficiente para o Serviço de Teleporte.";
					mes "A taxa de teleporte para "+.@destination$+" é de 200 Zenys.";
					close2;
					cutin "",255;
					end;
				}
				Zeny -= 200;
				RESRVPTS += 2;
				close2;
				warp .@destination$[1],.@coordinates[0],.@coordinates[1];
				end;
			case 2:
				close2;
				cutin "",255;
				end;
			}
		case 3:
			if (BaseClass != Job_Merchant) {
				mes "[Funcionária Kafra]";
				mes "Me desculpe ma o Aluguel de carrinho só está disponível para:";
				mes "Mercador, Ferreiro, Mestre-Ferreiro, Alquimísta, Criador, Mecânico e Bioquímico.";
				close2;
				cutin "",255;
				end;
			}
			if (checkcart()) {
				mes "[Funcionária Kafra]";
				mes "Você já tem um Carrinho.";
				mes "Infelizmente, nós só podemos ceder um carrinho por cada cliente.";
				close2;
				cutin "",255;
				end;
			}
			mes "[Funcionária Kafra]";
			mes "O preço para o aluguel é de 800 Zenys.";
			mes "Você gostaria de alugar um Carrinho?";
			next;
			switch (select("Alugar.:Cancelar")) {
			case 1:
				if (Zeny < 800) {
					mes "[Funcionária Kafra]";
					mes "Eu sinto muito, mas você não tem zenys o bastante para pagar o aluguel do Carrinho";
					mes "A taxa do aluguel é de 800 zeny.";
					close2;
					cutin "",255;
					end;
				}
				RESRVPTS += 8;
				Zeny -= 800;
				setcart;
				close2;
				cutin "",255;
				end;
			case 2:
				close2;
				cutin "",255;
				end;
			}
		case 4:
			mes "[Funcionária Kafra]";
			mes "Nós, aqui da Corporação Kafra, sempre estamos aqui para lhe proporcionar os melhores serviços.";
			mes "Nós esperamos satisfazer suas necessidades com padrões de excelência.";
			close2;
			cutin "",255;
			end;
		}
	}
	mes "[Funcionária Kafra]";
	mes "Eu só posso atender membros do Clã ^ff0000"+getguildname(.@GID)+"^000000.";
	mes "Se necessita usar os serviços encontre outra funcionária.";
	mes "E desculpe-me pela incoveniência.";
	close2;
	cutin "",255;
	end;
}

//============================================================
// WoE : Gerentes do Castelo
//============================================================
-	script	Castle Manager#cm::cm	-1,{

	.@GID = getcastledata(strnpcinfo(2),1);
	// .@guardiantype = Define os tipos de guardiões por base dos castelos.
	// 1 - Guardião Soldado, 2 - Guardião Arqueiro, 3 - Guardião Espadachim
	// .@guardianposx = Define o ponto x de invocação de cada guardião.
	// [0] = Ponto x do 1º guardião invocado.
	// .@guardianposy = Define o ponto y de invocação de cada guardião.
	// [0] = Ponto y do 1º guardião invocado.
	// .@masterroom = Define as coordenadas da Sala do Tesouro.
	if (strnpcinfo(2) == "aldeg_cas01") {
		setarray .@guardiantype[0],1,2,2,2,2,3,3,3;
		setarray .@guardianposx[0],17,39,38,45,21,218,213,73;
		setarray .@guardianposy[0],218,208,196,228,194,24,24,70;
		setarray .@masterroom[0],113,223;
	}
	else if (strnpcinfo(2) == "aldeg_cas02") {
		setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
		setarray .@guardianposx[0],27,88,117,60,51,21,36,210;
		setarray .@guardianposy[0],184,43,46,202,183,177,183,7;
		setarray .@masterroom[0],134,225;
	}
	else if (strnpcinfo(2) == "aldeg_cas03") {
		setarray .@guardiantype[0],3,3,1,1,1,2,2,2;
		setarray .@guardianposx[0],90,116,86,116,64,212,195,110;
		setarray .@guardianposy[0],112,112,120,76,103,160,151,217;
		setarray .@masterroom[0],229,267;
	}
	else if (strnpcinfo(2) == "aldeg_cas04") {
		setarray .@guardiantype[0],2,2,2,1,1,1,3,3;
		setarray .@guardianposx[0],187,192,148,145,169,198,48,55;
		setarray .@guardianposy[0],100,42,88,209,53,77,72,88;
		setarray .@masterroom[0],83,17;
	}
	else if (strnpcinfo(2) == "aldeg_cas05") {
		setarray .@guardiantype[0],2,2,1,1,3,3,3,3;
		setarray .@guardianposx[0],51,188,157,157,27,145,156,41;
		setarray .@guardianposy[0],202,79,192,74,221,78,73,112;
		setarray .@masterroom[0],64,8;
	}
	// Geffen (Britoniah)
	else if (strnpcinfo(2) == "gefg_cas01") {
		setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
		setarray .@guardianposx[0],67,184,62,36,50,50,189,200;
		setarray .@guardianposy[0],179,20,41,186,186,67,41,167;
		setarray .@masterroom[0],152,117;
	}
	else if (strnpcinfo(2) == "gefg_cas02") {
		setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
		setarray .@guardianposx[0],64,56,166,35,20,19,166,159;
		setarray .@guardianposy[0],168,41,25,148,150,41,42,188;
		setarray .@masterroom[0],145,115;
	}
	else if (strnpcinfo(2) == "gefg_cas03") {
		setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
		setarray .@guardianposx[0],48,113,48,157,243,157,234,238;
		setarray .@guardianposy[0],176,214,207,62,41,45,25,160;
		setarray .@masterroom[0],275,289;
	}
	else if (strnpcinfo(2) == "gefg_cas04") {
		setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
		setarray .@guardianposx[0],53,31,49,29,147,57,160,148;
		setarray .@guardianposy[0],191,178,220,46,65,46,50,189;
		setarray .@masterroom[0],116,123;
	}
	else if (strnpcinfo(2) == "gefg_cas05") {
		setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
		setarray .@guardianposx[0],45,71,72,66,177,66,177,193;
		setarray .@guardianposy[0],149,163,142,47,50,17,35,166;
		setarray .@masterroom[0],149,106;
	}
	// Payon (Baulder)
	else if (strnpcinfo(2) == "payg_cas01") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],229,225,222,99,65,36,51,138;
		setarray .@guardianposy[0],92,80,111,45,31,127,144,133;
		setarray .@masterroom[0],295,8;
	}
	else if (strnpcinfo(2) == "payg_cas02") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],237,228,210,57,42,287,264,27;
		setarray .@guardianposy[0],54,72,41,241,241,257,272,20;
		setarray .@masterroom[0],141,149;
	}
	else if (strnpcinfo(2) == "payg_cas03") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],245,269,36,41,39,19,37,268;
		setarray .@guardianposy[0],37,51,39,39,65,276,277,244;
		setarray .@masterroom[0],163,167;
	}
	else if (strnpcinfo(2) == "payg_cas04") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],251,232,231,32,32,35,36,270;
		setarray .@guardianposy[0],212,212,175,287,232,45,17,41;
		setarray .@masterroom[0],151,47;
	}
	else if (strnpcinfo(2) == "payg_cas05") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],19,33,266,266,263,263,250,36;
		setarray .@guardianposy[0],279,260,234,279,37,21,22,36;
		setarray .@masterroom[0],153,137;
	}
	// Prontera (Valkyrie Realms)
	else if (strnpcinfo(2) == "prtg_cas01") {
		setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
		setarray .@guardianposx[0],182,182,153,59,50,184,196,107;
		setarray .@guardianposy[0],62,116,86,28,36,183,189,179;
		setarray .@masterroom[0],15,209;
	}
	else if (strnpcinfo(2) == "prtg_cas02") {
		setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
		setarray .@guardianposx[0],161,153,178,71,49,64,7,75;
		setarray .@guardianposy[0],161,161,44,75,28,186,196,175;
		setarray .@masterroom[0],207,229;
	}
	else if (strnpcinfo(2) == "prtg_cas03") {
		setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
		setarray .@guardianposx[0],191,137,45,50,41,191,179,191;
		setarray .@guardianposy[0],190,190,99,87,87,42,43,72;
		setarray .@masterroom[0],190,130;
	}
	else if (strnpcinfo(2) == "prtg_cas04") {
		setarray .@guardiantype[0],3,3,3,1,1,1,2,2;
		setarray .@guardianposx[0],276,274,246,38,29,33,78,36;
		setarray .@guardianposy[0],14,35,246,240,240,258,48,61;
		setarray .@masterroom[0],275,160;
	}
	else if (strnpcinfo(2) == "prtg_cas05") {
		setarray .@guardiantype[0],3,3,3,1,1,1,2,2;
		setarray .@guardianposx[0],266,287,245,236,251,278,32,44;
		setarray .@guardianposy[0],262,280,250,63,63,253,253,248;
		setarray .@masterroom[0],281,176;
	}
	// Adicione seus castelos custominizados a partir daqui.
	else {
		end;
	}

	if (.@GID == 0) {
		mes "["+strnpcinfo(1)+"]";
		mes "Estou aguardando um mestre para cumprir meu destino.";
		mes "Que as almas valentes... lhe guie em seu futuro...";
		close;
	}
	if (getguildmaster(.@GID) != strcharinfo(0)) {
		mes "["+strnpcinfo(1)+"]";
		mes "Não importa o quanto você vai me incomodar, eu ainda vou seguir o mestre: ^ff0000"+getguildmaster(.@GID)+"^000000.";

		mes "Não importa quanto você me importuna, eu ainda seguirei meu mestre^ff0000"+getguildmaster(.@GID)+"^000000.";
		mes "Onde estão os Guardiões?!";
		mes "Expulsem ess"+(Sex?"e intruso":"a intrusa")+" daqui!";
		close;
	}
	mes "["+strnpcinfo(1)+"]";
	mes "Bem vindo"+(Sex?"o":"a")+". Honrado mestre, ^ff0000"+strcharinfo(0)+"^000000...";
	mes "Seu humilde servo, "+strnpcinfo(1)+", está aqui para lhe atender.";
	next;
	switch (select("Instruções do castelo:Investir no Crescimento Comercial:Investir nas Defesas do Castelo:Convocar Guardiões:Contrato com a Cop. Kafra:Ir para sala do mestre")) {
	case 1:
		mes "["+strnpcinfo(1)+"]";
		mes "["+strnpcinfo(1)+"]";
		mes "Mestre, vou relatar as informações do castelo.";
		mes " ";
		mes "^0000ffAgora, o nível de Crescimento Comercial é de "+getcastledata(strnpcinfo(2),2)+".";
		if (getcastledata(strnpcinfo(2),4)) {
			mes " Você investiu "+getcastledata(strnpcinfo(2),4)+" vezes no último dia.";
		}
		mes "Agora, o nível de Defesa do Castelo é de "+getcastledata(strnpcinfo(2),3)+".^000000";
		if (getcastledata(strnpcinfo(2),5)) {
			mes "^0000ff- Você investiu "+getcastledata(strnpcinfo(2),5)+" vezes no último dia.^000000";
		}
		mes " ";
		mes "Mestre, isso é tudo que tenho para relatar.";
		close;
	case 2:
		.@Economy = getcastledata(strnpcinfo(2),2);
		if (.@Economy < 6) { .@eco_invest = 5000; }
		else if ((.@Economy >= 6) && (.@Economy <= 10)) { .@eco_invest = 10000; }
		else if ((.@Economy >= 11) && (.@Economy <= 15)) { .@eco_invest = 20000; }
		else if ((.@Economy >= 16) && (.@Economy <= 20)) { .@eco_invest = 35000; }
		else if ((.@Economy >= 21) && (.@Economy <= 25)) { .@eco_invest = 55000; }
		else if ((.@Economy >= 26) && (.@Economy <= 30)) { .@eco_invest = 80000; }
		else if ((.@Economy >= 31) && (.@Economy <= 35)) { .@eco_invest = 110000; }
		else if ((.@Economy >= 36) && (.@Economy <= 40)) { .@eco_invest = 145000; }
		else if ((.@Economy >= 41) && (.@Economy <= 45)) { .@eco_invest = 185000; }
		else if ((.@Economy >= 46) && (.@Economy <= 50)) { .@eco_invest = 230000; }
		else if ((.@Economy >= 51) && (.@Economy <= 55)) { .@eco_invest = 280000; }
		else if ((.@Economy >= 56) && (.@Economy <= 60)) { .@eco_invest = 335000; }
		else if ((.@Economy >= 61) && (.@Economy <= 65)) { .@eco_invest = 395000; }
		else if ((.@Economy >= 66) && (.@Economy <= 70)) { .@eco_invest = 460000; }
		else if ((.@Economy >= 71) && (.@Economy <= 75)) { .@eco_invest = 530000; }
		else if ((.@Economy >= 76) && (.@Economy <= 80)) { .@eco_invest = 605000; }
		else if ((.@Economy >= 81) && (.@Economy <= 85)) { .@eco_invest = 685000; }
		else if ((.@Economy >= 86) && (.@Economy <= 90)) { .@eco_invest = 770000; }
		else if ((.@Economy >= 91) && (.@Economy <= 95)) { .@eco_invest = 860000; }
		else if ((.@Economy >= 96) && (.@Economy <= 100)) { .@eco_invest = 955000; }
		//Quadruplica o custo de investimento, se você já investiu uma vez.
		if (getcastledata(strnpcinfo(2),4)) {
			.@eco_invest *= 4;
		}
		mes "["+strnpcinfo(1)+"]";
		mes "Toda vez que você investe em crescimento comercial, a quantidade de bens produzido pelo clã aumentará.";
		mes "Investimentos serão uma necessidade, você deve considerar isso para o nosso futuro.";
		mes " ";
		mes "Inicialmente, você pode investir uma vez, mas se você pagar mais dinheiro, você poderá investir até duas vezes.";
		if (.@Economy >= 100) {
			mes " ";
			mes "^ff0000O nível de crescimento comercial de nosso Castelo está em 100%.";
			mes "De nenhum investimento mais é preciso.";
			mes "É dessa maneira que esperei que fosse de um grande economista como você, Mestre.^000000";
			close;
		}
		if (getcastledata(strnpcinfo(2),4) >= 2) {
			mes " ";
			mes "^ff0000Você já investiu duas vezes no crescimento comercial hoje.";
			mes "E não pode investir mais por enquanto.^000000";
			mes "Espero que as riquezas do Clã creça em um ritimo acelerado.";
			close;
		}
		if (getcastledata(strnpcinfo(2),4) == 0) {
			mes " ";
			mes "O custo total do investimento necessário atual é de ^ff0000"+.@eco_invest+"^000000 Zeny.";
			mes "Você vai investir?";
		}
		else {
			mes " ";
			mes "Você já investiu uma vez hoje...";
			mes "Se deseja investir mais, uma vez, será nescessário mais ^ff0000"+.@eco_invest+"^000000 Zeny.";
		}
		next;
		switch (select("Investir no crescimento:Cancelar")) {
		case 1:
			if (Zeny < .@eco_invest) {
				mes "["+strnpcinfo(1)+"]";
				mes "Me desculpe, mas não há Zeny suficiente para investir.";
				mes "Você pode tentar novamente quando tiver mais fundos.";
				close;
			}
			Zeny -= .@eco_invest;
			setcastledata strnpcinfo(2),4,getcastledata(strnpcinfo(2),4)+1;
			mes "[ "+strnpcinfo(1)+"]";
			mes "Nós terminamos o investimento seguramente.";
			mes "Eu espero que nosso nível de crescimento seja aumentado até amanhã.";
			close;
		case 2:
			mes "["+strnpcinfo(1)+"]";
			mes "Meu mestre, vou fazer como você solicita...";
			mes "Não há presa. Sei que faremos sempre o melhor.";
			close;
		}
	case 3:
		.@defence = getcastledata(strnpcinfo(2),3);
		if (.@defence < 6) { .@def_invest = 10000; }
		else if ((.@defence >= 6) && (.@defence <= 10)) { .@def_invest = 20000; }
		else if ((.@defence >= 11) && (.@defence <= 15)) { .@def_invest = 40000; }
		else if ((.@defence >= 16) && (.@defence <= 20)) { .@def_invest = 70000; }
		else if ((.@defence >= 21) && (.@defence <= 25)) { .@def_invest = 110000; }
		else if ((.@defence >= 26) && (.@defence <= 30)) { .@def_invest = 160000; }
		else if ((.@defence >= 31) && (.@defence <= 35)) { .@def_invest = 220000; }
		else if ((.@defence >= 36) && (.@defence <= 40)) { .@def_invest = 290000; }
		else if ((.@defence >= 41) && (.@defence <= 45)) { .@def_invest = 370000; }
		else if ((.@defence >= 46) && (.@defence <= 50)) { .@def_invest = 460000; }
		else if ((.@defence >= 51) && (.@defence <= 55)) { .@def_invest = 560000; }
		else if ((.@defence >= 56) && (.@defence <= 60)) { .@def_invest = 670000; }
		else if ((.@defence >= 61) && (.@defence <= 65)) { .@def_invest = 790000; }
		else if ((.@defence >= 66) && (.@defence <= 70)) { .@def_invest = 920000; }
		else if ((.@defence >= 71) && (.@defence <= 75)) { .@def_invest = 1060000; }
		else if ((.@defence >= 76) && (.@defence <= 80)) { .@def_invest = 1210000; }
		else if ((.@defence >= 81) && (.@defence <= 85)) { .@def_invest = 1370000; }
		else if ((.@defence >= 86) && (.@defence <= 90)) { .@def_invest = 1540000; }
		else if ((.@defence >= 91) && (.@defence <= 95)) { .@def_invest = 1720000; }
		else if ((.@defence >= 96) && (.@defence <= 100)) { .@def_invest = 1910000; }
		//Quadruplica o custo de investimento, se você já investiu uma vez.
		if (getcastledata(strnpcinfo(2),5)) {
			.@def_invest *= 4;
		}
		mes "["+strnpcinfo(1)+"]";
		mes "Se você investir nas defesas do Castelo, a durabilidade dos Guardiões e Emperium vão aumentar.";
		mes "Então, Se você considerar as batalhas que estão por vir, algum investimento nesta área será necessário.";
		mes " ";
		mes "Inicialmente, você pode investir uma vez, mas se você pagar mais dinheiro, você poderá investir até duas vezes.";
		if (.@defence >= 100) {
			mes " ";
			mes "^ff0000O nível de Defesa de nosso Castelo está em 100%.";
			mes "De nenhum investimento a mais é preciso.";
			mes "Foi assim que eu esperei de um grande estrategista como você, Mestre.^000000";
			close;
		}
		if (getcastledata(strnpcinfo(2),5) >= 2) {
			mes " ";
			mes "^ff0000Você já investiu duas vezes hoje.";
			mes "Você não pode fazer qualquer investimento a mais por enquanto.^000000";
			mes "Eu espero que as Defesas do nosso clã cresçam bem.";
			close;
		}
		if (getcastledata(strnpcinfo(2),5) == 0) {
			mes " ";
			mes "O custo de investimento atual é de ^ff0000"+.@def_invest+"^000000 Zeny.";
			mes "Você vai investir?";
		}
		else {
			mes " ";
			mes "Você já fez um investimento joje...";
			mes "Se investir mais uma vez será necessário mais ^ff0000"+.@def_invest+"^000000 Zenys.";
		}
		next;
		switch (select("Investir na Defesa.:Cancelar")) {
		case 1:
			if (Zeny < .@def_invest) {
				mes "["+strnpcinfo(1)+"]";
				mes "Eu sinto muito mas não há zenys o bastante para investir.";
				mes "Você pode tentar novamente quando tiver mais fundos, Mestre.";
				close;
			}
			Zeny -= .@def_invest;
			setcastledata strnpcinfo(2),5,getcastledata(strnpcinfo(2),5)+1;
			mes "[ "+strnpcinfo(1)+"]";
			mes "Nós terminamos o investimento com sucesso.";
			mes "Eu espero que nosso nível de Defesa do Castelo seja aumentado até amanhã.";
			close;
		case 2:
			mes "["+strnpcinfo(1)+"]";
			mes "Meu mestre, vou fazer como você solicita...";
			mes "Não há presa. Sei que faremos sempre o melhor.";
			close;
		}
	case 4:
		mes "["+strnpcinfo(1)+"]";
		mes "Você quer convocar um guardião?";
		mes "Será um protetor leal para nos defender.";
		mes "Por favor selecione um guardião para nos defender.";
		next;
		for (.@i = 0; .@i <= 7; ++.@i) {
			if (.@guardiantype[.@i] == 1) { .@type$ = "Guardião Soldado"; }
			else if (.@guardiantype[.@i] == 2) { .@type$ = "Guardião Arqueiro"; }
			else { .@type$ = "Guardian Knight"; }
			if (guardianinfo(strnpcinfo(2),.@i,0)) {
				setarray .@gname$[.@i], .@type$ + " - Implementado (" + guardianinfo(strnpcinfo(2),.@i,2) + "/" + guardianinfo(strnpcinfo(2),.@i,1) + ")";
			}
			else {
				setarray .@gname$[.@i], .@type$ + " - Não Implementado";
			}
		}
		.@menu$ = .@gname$[0]+":"+.@gname$[1]+":"+.@gname$[2]+":"+.@gname$[3]+":"+.@gname$[4]+":"+.@gname$[5]+":"+.@gname$[6]+":"+.@gname$[7];
		.@GDnum = select(.@menu$)+9;
		mes "["+strnpcinfo(1)+"]";
		mes "Você deseja convocar outro guardião?";
		mes "Serão necessários mais 10,000 zenys.";
		next;
		switch (select("Convocar:Cancelar")) {
		case 1:
			if (getgdskilllv(.@GID,10002) == 0) {
				mes "["+strnpcinfo(1)+"]";
				mes "Mestre, nós não temos os recursos para Chamar o Guardião.";
				mes "Se você quiser acumula-los, você tem que aprender a habilidade \"Comandar Guardiões\".";
				mes "Por isso não foi possível convocar o Guardião.";
				close;
			}
			if (getcastledata(strnpcinfo(2),.@GDnum) == 1) {
				mes "["+strnpcinfo(1)+"]";
				mes "Mestre, você já convocou esse Guardião.";
				mes "Não podemos convocar outro.";
				close;
			}
			if (Zeny < 10000) {
				mes "["+strnpcinfo(1)+"]";
				mes "Bem...";
				mes "Sinto muito, mas não temos recursos para convocar o Guardião.";
				mes "Por isso não foi possível convocar o Guardião.";
				close;
			}
			Zeny -= 10000;
			setcastledata strnpcinfo(2),.@GDnum,1; // Marca como 'instalado' o Guardião
			.@UseGID = .@GDnum - 10;
			if (.@guardiantype[.@UseGID] == 1) { .@type = 1287; }
			else if (.@guardiantype[.@UseGID] == 2) { .@type = 1285; }
			else { .@type = 1286; }
			guardian strnpcinfo(2),.@guardianposx[.@UseGID],.@guardianposy[.@UseGID],strmobinfo(2,.@type),.@type,"Guardião#"+strnpcinfo(2)+"::OnGuardianDied",.@UseGID;
			mes "["+strnpcinfo(1)+"]";
			mes "Completamos a convocação do Guardião.";
			mes "Nossas defesas serão reforçadas com ele.";
			close;
		case 2:
			mes "["+strnpcinfo(1)+"]";
			mes "Eu fiz como você ordenou.";
			mes "Mas por favor se lembre caso tenha dinheiro para poupado, será melhor covocar-lo.";
			close;
		}
	case 5:
		if (getcastledata(strnpcinfo(2),9) == 1) {
			mes "["+strnpcinfo(1)+"]";
			mes "No momento contratamos uma Funcionária Kafra...";
			mes "Deseja demitir a Funcionária Kafra?";
			next;
			switch (select("Demitir:Cancelar")) {
			case 1:
				cutin "kafra_01",2;
				mes "[Funcionária Kafra]";
				mes "Eu trabalhei tão duro... Como você pode fazer isso, Mestre?...";
				mes "Por favor...";
				mes "Reconsidere...";
				mes "Me dê outra chance, Mastre... Por favor...";
				next;
				switch (select("Demitir:Cancelar")) {
				case 1:
					mes "[Funcionária Kafra]";
					mes "Oh, meu deus!";
					mes "Isso é um absurdo!";
					next;
					cutin "",255;
					break;
				case 2:
					mes "[Funcionária Kafra]";
					mes "Eu vou trabalhar duro para você...";
					mes "Obrigado!";
					close;
				}
				break;
			case 2:
				mes "["+strnpcinfo(1)+"]";
				mes "Na minha opnião, ela trabalhou duro.";
				mes "Foi uma boa decisão em mantê-la.";
				close;
			}
			disablenpc "Funcionária Kafra#"+strnpcinfo(2);
			setcastledata strnpcinfo(2),9,0;
			mes "["+strnpcinfo(1)+"]";
			mes "....";
			mes "Eu despachei a funcionária Kafra...";
			mes "Mas... você está insatisfeito com algo?";
			close;
		}
		mes "["+strnpcinfo(1)+"]";
		mes "Deseja assinar um contrato com a central da Kafra para trazer uma funcionária para nosso castelo?";
		mes "^ff0000 10,000 zenys serão precisos para esses serviço. ";
		next;
		switch (select("Contratar.:Cancelar")) {
		case 1:
			if (getgdskilllv(.@GID,10001) == 0) {
				mes "["+strnpcinfo(1)+"]";
				mes "Mestre, nós não podemos contratar uma Funcionária de Kafra porque nós não temos um contrato com o Escritório central de Kafra.";
				mes "Se você quiser obter um contrato com o Escritório central de Kafra, nosso clã precisa aprender a habilidade \"Contato com a Kafra\".";
				close;
			}
			if (Zeny < 10000) {
				mes "["+strnpcinfo(1)+"]";
				mes "Bem... eu sinto muito mas nós não temos fundos o bastante para contratar uma Funcionária Kafra.";
				close;
			}
			Zeny -= 10000;
			enablenpc "Funcionária Kafra#"+strnpcinfo(2);
			setcastledata strnpcinfo(2),9,1;
			mes "["+strnpcinfo(1)+"]";
			mes "Nós obtivemos um contrato com o Escritório central de kafra.";
			next;
			cutin "kafra_01",2;
			mes "[Funcionária Kafra]";
			mes "Olá, como você está?";
			mes "Eu fui despachada do Escritório central da Kafra.";
			mes "Farei o meu melhor não manchar a reputação do nosso Clã.";
			next;
			cutin "",255;
			mes "["+strnpcinfo(1)+"]";
			mes "As condições de Contrato da Funcionária Kafra é de 1 mês e depois deste termo, você precisará pagar uma taxa adicional.";
			mes "Ela será muito útil para nossos membros";
			close;
		case 2:
			mes "["+strnpcinfo(1)+"]";
			mes "Eu fiz como você ordenou, mas alguns de nossos membros não ficarão infelizes.";
			mes "Será melhor contratar uma Funcionária de Kafra o quanto antes.";
			close;
		}
	case 6:
		mes "["+strnpcinfo(1)+"]";
		mes "Você quer visitar o quarto onde nossos tesouros são armazenados?";
		mes "Aquele quarto é restringido a você... você é o único com acesso.";
		next;
		switch (select("Ir para sala do mestre.:Cancelar")) {
		case 1:
			mes "["+strnpcinfo(1)+"]";
			mes "Eu lhe mostrarei o caminho secreto.";
			mes "Por favor... siga-me.";
			mes "Quando você quiser voltar para cá, aperte o interruptor secreto.";
			close2;
			warp strnpcinfo(2),.@masterroom[0],.@masterroom[1];
			end;
		case 2:
			mes "["+strnpcinfo(1)+"]";
			mes "Os tesouros são produzidos uma vez por dia... se você não os remove-los a tempo, eles não serão produzidos mais.";
			mes "Então, será melhor se você verificasse eles de vez em quando.";
			close;
		}
	}
}

//============================================================
// WoE : Entrada para o calabouço
//============================================================
-	script	Alavanca#gd::gdlever	-1,{

	.@GID = getcastledata(strnpcinfo(2),1);

	if (.@GID == 0) {
		mes "[Voz Sussurrando]";
		mes "'Aqueles que superam sua aprovação mostraram muita coragem... ainda vão achar outra aprovação pelo caminho.'";
		close;
	}

	if (compare(strnpcinfo(2),"aldeg")) {
		.@destination$ = "gld_dun02";
		if (compare(strnpcinfo(2),"cas01")) setarray .@coordinates[0],32,122;
		else if (compare(strnpcinfo(2),"cas02")) setarray .@coordinates[0],79,30;
		else if (compare(strnpcinfo(2),"cas03")) setarray .@coordinates[0],165,38;
		else if (compare(strnpcinfo(2),"cas04")) setarray .@coordinates[0],160,148;
		else if (compare(strnpcinfo(2),"cas05")) setarray .@coordinates[0],103,169;
	}
	else if (compare(strnpcinfo(2),"gefg")) {
		.@destination$ = "gld_dun04";
		if (compare(strnpcinfo(2),"cas01")) setarray .@coordinates[0],39,258;
		else if (compare(strnpcinfo(2),"cas02")) setarray .@coordinates[0],125,270;
		else if (compare(strnpcinfo(2),"cas03")) setarray .@coordinates[0],268,251;
		else if (compare(strnpcinfo(2),"cas04")) setarray .@coordinates[0],268,108;
		else if (compare(strnpcinfo(2),"cas05")) setarray .@coordinates[0],230,35;
	}
	else if (compare(strnpcinfo(2),"payg")) {
		.@destination$ = "gld_dun01";
		if (compare(strnpcinfo(2),"cas01")) setarray .@coordinates[0],186,165;
		else if (compare(strnpcinfo(2),"cas02")) setarray .@coordinates[0],54,165;
		else if (compare(strnpcinfo(2),"cas03")) setarray .@coordinates[0],54,39;
		else if (compare(strnpcinfo(2),"cas04")) setarray .@coordinates[0],186,39;
		else if (compare(strnpcinfo(2),"cas05")) setarray .@coordinates[0],223,202;
	}
	else if (compare(strnpcinfo(2),"prtg")) {
		.@destination$ = "gld_dun03";
		if (compare(strnpcinfo(2),"cas01")) setarray .@coordinates[0],28,251;
		else if (compare(strnpcinfo(2),"cas02")) setarray .@coordinates[0],164,268;
		else if (compare(strnpcinfo(2),"cas03")) setarray .@coordinates[0],164,179;
		else if (compare(strnpcinfo(2),"cas04")) setarray .@coordinates[0],268,203;
		else if (compare(strnpcinfo(2),"cas05")) setarray .@coordinates[0],199,28;
	}
	// Adicione seus castelos custominizados a partir daqui.
	else {
		end;
	}

	mes "[Voz Sussurrando]";
	mes "'Só o verdadeiramente valente podem fazer a prova.'";
	next;
	mes " ";
	mes "Há uma alavanca pequena. Puxar isto?";
	next;
	if(select("Pull.:Don't pull.") == 1) {
		if (getcharid(2) == .@GID) {
			close2;
			warp .@destination$,.@coordinates[0],.@coordinates[1];
			end;
		}
		mes " ";
		mes "nada Aconteceu.";
		close;
	}
	close;
}

//============================================================
// Controle sobre a invocação dos Guardiões
//============================================================
-	script	Gld_Guard_Template::Gld_Guard_Template	-1,{
// Invocação de Guardiões nos castelos
// Ao adicionar novos castelos, certifique-se que as coordenadas coincidem
// com as coordenadas definidas na Estrutura do Gerente do Castelo.
OnSpawnGuardians:
	// Define os tipos de guardiões em uma base do castelo.
	// 1 - Guardião Soldado; 2 - Guardião Arqueiro; 3 - Guardião Espadachim
	// Definir o ponto x de invocação para cada guardião.
	// [0] = Ponto de invocação do 1º guardião.
	// Definir o ponto y de invocação para cada guardião.
	// [0] = Ponto de invocação do 1º guardião.

	// Aldebaran (Luina)
	if (strnpcinfo(2) == "aldeg_cas01") {
		setarray .@guardiantype[0],1,2,2,2,2,3,3,3;
		setarray .@guardianposx[0],17,39,38,45,21,218,213,73;
		setarray .@guardianposy[0],218,208,196,228,194,24,24,70;
	}
	else if (strnpcinfo(2) == "aldeg_cas02") {
		setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
		setarray .@guardianposx[0],27,88,117,60,51,21,36,210;
		setarray .@guardianposy[0],184,43,46,202,183,177,183,7;
	}
	else if (strnpcinfo(2) == "aldeg_cas03") {
		setarray .@guardiantype[0],3,3,1,1,1,2,2,2;
		setarray .@guardianposx[0],90,116,86,116,64,212,195,110;
		setarray .@guardianposy[0],112,112,120,76,103,160,151,217;
	}
	else if (strnpcinfo(2) == "aldeg_cas04") {
		setarray .@guardiantype[0],2,2,2,1,1,1,3,3;
		setarray .@guardianposx[0],187,192,148,145,169,198,48,55;
		setarray .@guardianposy[0],100,42,88,209,53,77,72,88;
	}
	else if (strnpcinfo(2) == "aldeg_cas05") {
		setarray .@guardiantype[0],2,2,1,1,3,3,3,3;
		setarray .@guardianposx[0],51,188,157,157,27,145,156,41;
		setarray .@guardianposy[0],202,79,192,74,221,78,73,112;
	}
	// Geffen (Britoniah)
	else if (strnpcinfo(2) == "gefg_cas01") {
		setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
		setarray .@guardianposx[0],67,184,62,36,50,50,189,200;
		setarray .@guardianposy[0],179,20,41,186,186,67,41,167;
	}
	else if (strnpcinfo(2) == "gefg_cas02") {
		setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
		setarray .@guardianposx[0],64,56,166,35,20,19,166,159;
		setarray .@guardianposy[0],168,41,25,148,150,41,42,188;
	}
	else if (strnpcinfo(2) == "gefg_cas03") {
		setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
		setarray .@guardianposx[0],48,113,48,157,243,157,234,238;
		setarray .@guardianposy[0],176,214,207,62,41,45,25,160;
	}
	else if (strnpcinfo(2) == "gefg_cas04") {
		setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
		setarray .@guardianposx[0],53,31,49,29,147,57,160,148;
		setarray .@guardianposy[0],191,178,220,46,65,46,50,189;
	}
	else if (strnpcinfo(2) == "gefg_cas05") {
		setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
		setarray .@guardianposx[0],45,71,72,66,177,66,177,193;
		setarray .@guardianposy[0],149,163,142,47,50,17,35,166;
	}
	// Payon (Baulder)
	else if (strnpcinfo(2) == "payg_cas01") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],229,225,222,99,65,36,51,138;
		setarray .@guardianposy[0],92,80,111,45,31,127,144,133;
	}
	else if (strnpcinfo(2) == "payg_cas02") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],237,228,210,57,42,287,264,27;
		setarray .@guardianposy[0],54,72,41,241,241,257,272,20;
	}
	else if (strnpcinfo(2) == "payg_cas03") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],245,269,36,41,39,19,37,268;
		setarray .@guardianposy[0],37,51,39,39,65,276,277,244;
	}
	else if (strnpcinfo(2) == "payg_cas04") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],251,232,231,32,32,35,36,270;
		setarray .@guardianposy[0],212,212,175,287,232,45,17,41;
	}
	else if (strnpcinfo(2) == "payg_cas05") {
		setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
		setarray .@guardianposx[0],19,33,266,266,263,263,250,36;
		setarray .@guardianposy[0],279,260,234,279,37,21,22,36;
	}
	// Prontera (Valkyrie Realms)
	else if (strnpcinfo(2) == "prtg_cas01") {
		setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
		setarray .@guardianposx[0],182,182,153,59,50,184,196,107;
		setarray .@guardianposy[0],62,116,86,28,36,183,189,179;
	}
	else if (strnpcinfo(2) == "prtg_cas02") {
		setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
		setarray .@guardianposx[0],161,153,178,71,49,64,7,75;
		setarray .@guardianposy[0],161,161,44,75,28,186,196,175;
	}
	else if (strnpcinfo(2) == "prtg_cas03") {
		setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
		setarray .@guardianposx[0],191,137,45,50,41,191,179,191;
		setarray .@guardianposy[0],190,190,99,87,87,42,43,72;
	}
	else if (strnpcinfo(2) == "prtg_cas04") {
		setarray .@guardiantype[0],3,3,3,1,1,1,2,2;
		setarray .@guardianposx[0],276,274,246,38,29,33,78,36;
		setarray .@guardianposy[0],14,35,246,240,240,258,48,61;
	}
	else if (strnpcinfo(2) == "prtg_cas05") {
		setarray .@guardiantype[0],3,3,3,1,1,1,2,2;
		setarray .@guardianposx[0],266,287,245,236,251,278,32,44;
		setarray .@guardianposy[0],262,280,250,63,63,253,253,248;
	}
	// Adicione seus castelos custominizados a partir daqui.
	else {
		end;
	}

	// Elimina todos guardiões já invocados antes da novas invocações.
	// Isso deve previnir a duplicações de Guardiões antes de desconectar do Server.
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnGuardianDied";

	for (.@i = 0; .@i <= 7; ++.@i) {
		.@UseGID = .@i + 10;
		if (.@guardiantype[.@i] == 1) { .@type = 1287; }
		else if (.@guardiantype[.@i] == 2) { .@type = 1285; }
		else { .@type = 1286; }
		if (getcastledata(strnpcinfo(2),.@UseGID)) {
			guardian strnpcinfo(2),.@guardianposx[.@i],.@guardianposy[.@i],strmobinfo(2,.@type),.@type,"Guardião#"+strnpcinfo(2)+"::OnGuardianDied",.@i;
		}
	}
	end;

OnGuardianDied:
	end;
}

//============================================================
// Controle sobre a invocação dos tesouros
//============================================================
-	script	Gld_Trea_Spawn::Gld_Trea_Spawn	-1,{
	end;

OnClock0001:
	if (strnpcinfo(1) == "Gld_Trea_Spawn") end;

	.@GID = getcastledata(strnpcinfo(2),1);

	// Se não houver nenhum dono, não fazer nada.
	if (!.@GID) end;

	// Verifica como está a atual economia
	.@Treasure = getcastledata(strnpcinfo(2),2)/5+4;

	// Pega as informações e envia as coordenadas
	if (strnpcinfo(2) == "aldeg_cas01") {
		.@treasurebox = 1324;
		setarray .@treasurex[0],115,122,115,122,116,117,118,119,120,121,121,121,121,121,121,120,119,118,117,116,116,116,116,116;
		setarray .@treasurey[0],226,226,219,219,225,225,225,225,225,225,224,223,222,221,220,220,220,220,220,220,221,222,223,224;
	}
	else if (strnpcinfo(2) == "aldeg_cas02") {
		.@treasurebox = 1326;
		setarray .@treasurex[0],134,135,135,134,132,133,134,135,136,137,137,137,137,137,137,136,135,134,133,132,132,132,132,132;
		setarray .@treasurey[0],231,231,230,230,233,233,233,233,233,233,232,231,230,229,228,228,228,228,228,228,229,230,231,232;
	}
	else if (strnpcinfo(2) == "aldeg_cas03") {
		.@treasurebox = 1328;
		setarray .@treasurex[0],224,225,225,224,222,223,224,225,226,227,227,227,227,227,227,226,225,224,223,222,222,222,222,222;
		setarray .@treasurey[0],269,269,268,268,271,271,271,271,271,271,270,269,268,267,266,266,266,266,266,266,267,268,269,270;
	}
	else if (strnpcinfo(2) == "aldeg_cas04") {
		.@treasurebox = 1330;
		setarray .@treasurex[0],84,85,85,84,82,83,84,85,86,87,87,87,87,87,87,86,85,84,83,82,82,82,82,82;
		setarray .@treasurey[0],13,13,12,12,15,15,15,15,15,15,14,13,12,11,10,10,10,10,10,10,11,12,13,14;
	}
	else if (strnpcinfo(2) == "aldeg_cas05") {
		.@treasurebox = 1332;
		setarray .@treasurex[0],61,62,62,61,59,60,61,62,63,64,64,64,64,64,64,63,62,61,60,59,59,59,59,59;
		setarray .@treasurey[0],12,12,11,11,14,14,14,14,14,14,13,12,11,10,9,9,9,9,9,9,10,11,12,13;
	}
	else if (strnpcinfo(2) == "gefg_cas01") {
		.@treasurebox = 1334;
		setarray .@treasurex[0],153,154,154,153,151,152,153,154,155,156,156,156,156,156,156,155,154,153,152,151,151,151,151,151;
		setarray .@treasurey[0],113,113,112,112,115,115,115,115,115,115,114,113,112,111,110,110,110,110,110,110,111,112,113,114;
	}
	else if (strnpcinfo(2) == "gefg_cas02") {
		.@treasurebox = 1336;
		setarray .@treasurex[0],139,140,140,139,137,138,139,140,141,142,142,142,142,142,142,141,140,139,138,137,137,137,137,137;
		setarray .@treasurey[0],115,115,114,114,117,117,117,117,117,117,116,115,114,113,112,112,112,112,112,112,113,114,115,116;
	}
	else if (strnpcinfo(2) == "gefg_cas03") {
		.@treasurebox = 1338;
		setarray .@treasurex[0],269,270,270,269,267,268,269,270,271,272,272,272,272,272,272,271,270,269,268,267,267,267,267,267;
		setarray .@treasurey[0],291,291,290,290,293,293,293,293,293,293,292,291,290,289,288,288,288,288,288,288,289,290,291,292;
	}
	else if (strnpcinfo(2) == "gefg_cas04") {
		.@treasurebox = 1340;
		setarray .@treasurex[0],115,116,116,115,113,114,115,116,117,118,118,118,118,118,118,117,116,115,114,113,113,113,113,113;
		setarray .@treasurey[0],119,119,118,118,121,121,121,121,121,121,120,119,118,117,116,116,116,116,116,116,117,118,119,120;
	}
	else if (strnpcinfo(2) == "gefg_cas05") {
		.@treasurebox = 1342;
		setarray .@treasurex[0],143,144,144,143,141,142,143,144,145,146,146,146,146,146,146,145,144,143,142,141,141,141,141,141;
		setarray .@treasurey[0],110,110,109,109,112,112,112,112,112,112,111,110,109,108,107,107,107,107,107,107,108,109,110,111;
	}
	else if (strnpcinfo(2) == "payg_cas01") {
		.@treasurebox = 1344;
		setarray .@treasurex[0],289,292,292,289,288,289,290,291,292,293,293,293,293,293,293,292,291,290,289,288,288,288,288,288;
		setarray .@treasurey[0],10,10,7,7,11,11,11,11,11,11,10,9,8,7,6,6,6,6,6,6,7,8,9,10;
	}
	else if (strnpcinfo(2) == "payg_cas02") {
		.@treasurebox = 1346;
		setarray .@treasurex[0],143,146,146,143,142,143,144,145,146,147,147,147,147,147,147,146,145,144,143,142,142,142,142,142;
		setarray .@treasurey[0],146,146,143,143,147,147,147,147,147,147,146,145,144,143,142,142,142,142,142,142,143,144,145,146;
	}
	else if (strnpcinfo(2) == "payg_cas03") {
		.@treasurebox = 1348;
		setarray .@treasurex[0],158,159,159,158,156,157,158,159,160,161,161,161,161,161,161,160,159,158,157,156,156,156,156,156;
		setarray .@treasurey[0],169,169,168,168,171,171,171,171,171,171,170,169,168,167,166,166,166,166,166,166,167,168,169,170;
	}
	else if (strnpcinfo(2) == "payg_cas04") {
		.@treasurebox = 1350;
		setarray .@treasurex[0],146,147,147,146,144,145,146,147,148,149,149,149,149,149,149,148,147,146,145,144,144,144,144,144;
		setarray .@treasurey[0],48,48,47,47,50,50,50,50,50,50,49,48,47,46,45,45,45,45,45,45,46,47,48,49;
	}
	else if (strnpcinfo(2) == "payg_cas05") {
		.@treasurebox = 1352;
		setarray .@treasurex[0],155,158,158,155,154,155,156,157,158,159,159,159,159,159,159,158,157,156,155,154,154,154,154,154;
		setarray .@treasurey[0],134,134,131,131,135,135,135,135,135,135,134,133,132,131,130,130,130,130,130,130,131,132,133,134;
	}
	else if (strnpcinfo(2) == "prtg_cas01") {
		.@treasurebox = 1354;
		setarray .@treasurex[0],10,11,11,10,8,9,10,11,12,13,13,13,13,13,13,12,11,10,9,8,8,8,8,8;
		setarray .@treasurey[0],209,209,208,208,211,211,211,211,211,211,210,209,208,207,206,206,206,206,206,206,207,208,209,210;
	}
	else if (strnpcinfo(2) == "prtg_cas02") {
		.@treasurebox = 1356;
		setarray .@treasurex[0],201,202,202,201,199,200,201,202,203,204,204,204,204,204,204,203,202,201,200,199,199,199,199,199;
		setarray .@treasurey[0],228,228,227,227,230,230,230,230,230,230,229,228,227,226,225,225,225,225,225,225,226,227,228,229;
	}
	else if (strnpcinfo(2) == "prtg_cas03") {
		.@treasurebox = 1358;
		setarray .@treasurex[0],187,188,188,187,185,186,187,188,189,190,190,190,190,190,190,189,188,187,186,185,185,185,185,185;
		setarray .@treasurey[0],132,132,131,131,134,134,134,134,134,134,133,132,131,130,129,129,129,129,129,129,130,131,132,133;
	}
	else if (strnpcinfo(2) == "prtg_cas04") {
		.@treasurebox = 1360;
		setarray .@treasurex[0],269,270,270,269,267,268,269,270,271,272,272,272,272,272,272,271,270,269,268,267,267,267,267,267;
		setarray .@treasurey[0],162,162,161,161,164,164,164,164,164,164,163,162,161,160,159,159,159,159,159,159,160,161,162,163;
	}
	else if (strnpcinfo(2) == "prtg_cas05") {
		.@treasurebox = 1362;
		setarray .@treasurex[0],275,276,276,275,273,274,275,276,277,278,278,278,278,278,278,277,276,275,274,273,273,273,273,273;
		setarray .@treasurey[0],178,178,177,177,180,180,180,180,180,180,179,178,177,176,175,175,175,175,175,175,176,177,178,179;
	}
	// Adicione seus castelos custominizados a partir daqui.
	else {
		end;
	}

	// Aplicar investimento nas Economias e Defesas. Só acontece se houver investimentos realizados.
	// Desenvolvimento permanente só deve acontecer uma vez por dia.
	if (getcastledata(strnpcinfo(2),4)) {
		.@Economy = getcastledata(strnpcinfo(2),2);
		setcastledata strnpcinfo(2),2,.@Economy + getcastledata(strnpcinfo(2),4) + (rand(2) && getgdskilllv(.@GID,10014));
		if (getcastledata(strnpcinfo(2),2) > 100) setcastledata strnpcinfo(2),2,100;
	}
	if (getcastledata(strnpcinfo(2),5)) {
		.@defence = getcastledata(strnpcinfo(2),3);
		setcastledata strnpcinfo(2),3,.@defence + getcastledata(strnpcinfo(2),5);
		if (getcastledata(strnpcinfo(2),3) > 100) setcastledata strnpcinfo(2),3,100;
	}
	// Redefinição de limites de investimentos diários.
	setcastledata strnpcinfo(2),4,0;
	setcastledata strnpcinfo(2),5,0;

	// Invocar caixas do tesouro na ordem correta.
	for (.@i = 0; .@i < .@Treasure ; ++.@i) {
		.@boxid = .@treasurebox + (.@i+2) % 2;
		.@box = 1 << .@i;
		// Não invoca baús se eles já existem.
		if ((getd("$@"+strnpcinfo(2)+"_treasure") & .@box) == 0) {
			monster strnpcinfo(2),.@treasurex[.@i],.@treasurey[.@i],"Baú do Tesouro",.@boxid,1,"Treasure#"+strnpcinfo(2)+"::OnTreasureDied"+.@i;
			setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") | .@box;
		}
	}
	end;

//--------------------------------------------------------------
// Eventos individuais 'Você matou o Baú' para garantir
// invocações adequadas na mudança diária.
//--------------------------------------------------------------
OnTreasureDied0:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~1;
	end;
OnTreasureDied1:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~2;
	end;
OnTreasureDied2:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~4;
	end;
OnTreasureDied3:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~8;
	end;
OnTreasureDied4:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~16;
	end;
OnTreasureDied5:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~32;
	end;
OnTreasureDied6:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~64;
	end;
OnTreasureDied7:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~128;
	end;
OnTreasureDied8:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~256;
	end;
OnTreasureDied9:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~512;
	end;
OnTreasureDied10:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~1024;
	end;
OnTreasureDied11:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~2048;
	end;
OnTreasureDied12:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~4096;
	end;
OnTreasureDied13:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~8192;
	end;
OnTreasureDied14:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~16384;
	end;
OnTreasureDied15:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~32768;
	end;
OnTreasureDied16:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~65536;
	end;
OnTreasureDied17:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~131072;
	end;
OnTreasureDied18:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~262144;
	end;
OnTreasureDied19:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~524288;
	end;
OnTreasureDied20:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~1048576;
	end;
OnTreasureDied21:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~2097152;
	end;
OnTreasureDied22:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~4194304;
	end;
OnTreasureDied23:
	setd "$@"+strnpcinfo(2)+"_treasure",getd("$@"+strnpcinfo(2)+"_treasure") & ~8388608;
	end;
}

