//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//	                   Scripts                                   ||
//===============================================================||
// = Nome do NPC:                                                ||
// - gatuno.txt                                                  ||
//===============================================================||
// = Criação:                                                    ||
// - eA/Herc/Cronus                                              ||
//===============================================================||
// = Versão:                                                     ||
// - 1.0                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Tradução [Revisões Cronus] / [SlexFire]                     ||
//===============================================================||
// = Descrição:                                                  ||
// - NPC de troca de classe gatuno PRE-RE                        ||
//===============================================================//

moc_prydb1,39,129,2	script	Guia de Gatuno	1_F_04,{
	if (Upper == 1) {
		if (ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) {
			if (Class == Job_Novice_High) {
				mes "[Guia de Gatuno]";
				mes "Huh? Eu te conheço? É assustador que você me parece tão familiar. Você não tem um irmão gêmeo, não é?";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Guia de Gatuno]";
					mes "O quê, você quer ser um Gatuno? Sinto muito, mas parece que você precisa de mais treinamento.";
					next;
					mes "[Guia de Gatuno]";
					mes "Não tenha pressa e aprenda todas as Habilidades Básicas, você vai? Ok então, vejo você mais tarde~!";
					close;
				}
				mes "[Guia de Gatuno]";
				mes "Bem, eu tive o pressentimento que você teve uma vida de lutas, então estou te promovendo para um Gatuno neste minuto. É melhor eu dar o que você quer...";
				next;
				skill 143,0,0;
				jobchange Job_Thief_High;
				skill 149,1,0;
				skill 150,1,0;
				skill 151,1,0;
				skill 152,1,0;
				mes "[Guia de Gatuno]";
				mes "Desde que você se tornar um Gatuno, viver como um Gatuno. Agora, vá por isso! Próximo~";
				close;
			}
			else {
				mes "[Guia de Gatuno]";
				if (Sex)
					mes "Ei, cara.";
				else
					mes "Ei, bebê~";
				close;
			}
		}
		else {
			mes "[Guia de Gatuno]";
			if (Sex)
				mes "Ei, cara.";
			else
				mes "Ei, bebê.";
			mes "...Ei! Você parece muito cuti-cuti para querer ser um Gatuno! Agora suma, estou ocupado. Próximo!";
			close;
		}
	}
	if (BaseJob == Job_Thief) {
		mes "[Guia de Gatuno]";
		mes "Se você tem um problema, sinta-se a vontade para falar comigo quando quiser, ok?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Guia de Gatuno]";
		mes "Que diabos...?";
		switch(Class) {
		case 1:
			mes "Huh.";
			mes "Agora, isso é uma grande espada.";
			next;
			mes "[Guia de Gatuno]";
			mes "Então...";
			mes "Tentando ficar a par de alguma coisa... Amigo?";
			break;
		case 2:
			mes "O que um Mago está fazendo aqui? Você não deveria estar fazendo truques com cartas em outro local? Oh bem, esse é um país livre...";
			next;
			mes "[Guia de Gatuno]";
			mes "Oh espere, não é não... Caia fora daqui!";
			break;
		case 3:
			mes "Cara, vocês Arqueiros não deveriam estar caçando na floresta ou algo do tipo?";
			break;
		case 4:
			mes "Você sabe que roubamos para viver, certo? O que você está fazendo nesse tipo de lugar, Noviço?";
			break;
		case 5:
			mes "Você é um Mercador, certo? Por que você está andando em um covil de Gatunos?!";
			next;
			mes "[Guia de Gatuno]";
			mes "Parece que você está nos tentando para roubarmos você! Vamos, corra e caia fora daqui~";
			break;
		case 8:
			mes "Oh meu Deus... Estou morrendo?";
			next;
			mes "[Guia de Gatuno]";
			mes "Por que mais um Sacerdote viria aqui? Eu acho que é melhor eu começar a confessar todos meus pecados.";
			close;
		case 12:
			mes "Você não era um de nós?! Cara, você mudou. Você parece realmente perigoso agora...";
			close;
		case 17:
			mes "Cara, você ficou realmente frio de repente! Você deve ter algumas habilidades que eu sonho ter um dia!";
			close;
		}
		next;
		mes "[Guia de Gatuno]";
		mes "*Suspiro* Olha, não há realmente nenhuma necessidade para você estar neste tipo de lugar. Você deveria seguir seu caminho...";
		close;
	}
	if (job_thief_q == 3 && countitem(1069) > 0 || countitem(1070) > 0) {
		mes "[Guia de Gatuno]";
		mes "Hmmm?";
		mes "Você ajuntou os Cogumelos para o Teste de Gatuno, certo?";
		next;
		mes "[Guia de Gatuno]";
		mes "Aqui, fale com o outro cara perto de mim. Ele que verifica os Cogumelos.";
		close;
	}
	else if (job_thief_q == 3) {
		mes "[Guia de Gatuno]";
		mes "Então como foi a plantação de Cogumelos? Foi divertido?";
		next;
		if (select("Sim, bem legal.:Aquilo foi horrível.") == 1) {
			mes "[Guia de Gatuno]";
			mes "Heh heh! Boa atitude. Em nosso trabalho, você precisa curtir suas mãos ficando sujas, de uma forma ou outra.";
			close;
		}
		mes "[Guia de Gatuno]";
		mes "Sim? Eu estive lá também, então eu posso ver o porque daquele lugar não ser o copo de chá de ninguém. Ainda, ser um Gatuno não é uma vida de glamour e moda notura.";
		close;
	}
	if (job_thief_q == 2) {
		mes "[Guia de Gatuno]";
		mes "Ei, o que você está fazendo aqui? Você não deveria estar pegando Cogumelos? Ou você precisa que eu explique tudo denovo?";
		next;
		if  (select("Sim.:Não, tudo bem.") == 1) {
			mes "[Guia de Gatuno]";
			mes "*Suspiro* Bem, há sempre um no bando. Certo, escute cuidadosamente.";
			next;
			mes "[Guia de Gatuno]";
			mes "Vamos lá, para seu teste, você precisa roubar Cogumelos de uma plantação. Não se preocupe, o cara que é dono da fazenda merece ser roubado.";
			next;
			mes "[Guia de Gatuno]";
			mes "Enfim, você tem que juntar dois tipos de Cogumelos: ^0000FFCogumelo Venenoso^000000 e ^0000FFCogumelo Borrachento^000000.";
			next;
			mes "[Guia de Gatuno]";
			mes "Tenha cuidado, pois existem monstros na plantação que estão lá para proteger os Cogumelos. Então isso não será uma caminhada no parque.";
			next;
			mes "[Guia de Gatuno]";
			mes "Quando você voltar aqui depois de juntar os Cogumelos, você será pontuado de acordo com os Cogumelos que coletou.";
			next;
			mes "[Guia de Gatuno]";
			mes "Cada Cogumelo Venenoso vale 3 pontos, e você recebe 1 ponto por cada Cogumelo Borrachento. Você precisa de 25 pontos para passar no teste.";
			next;
			mes "[Guia de Gatuno]";
			mes "Vá lá fora e continue indo até o campo oriental das Pirâmides. E você verá um de nossos camaradas entre duas colunas.";
			next;
			mes "[Guia de Gatuno]";
			mes "Fale com aquele cara, e ele te levará para o plantação pela porta dos fundos.";
			next;
			mes "[Guia de Gatuno]";
			mes "Naquele campo, eu acho que as coordenadas são '^FF0000141, 125^000000.' Apenas digite ^3355FF/where^000000 no canto direito da caixa de chat para checar suas coordenadas atuais.";
			close;
		}
		mes "[Guia de Gatuno]";
		mes "Huh. Por um segundo ali, eu pensei que você tinha algo realmente importante para me dizer.";
		close;
	}
	mes "[Guia de Gatuno]";
	if (job_thief_q == 0) {
		mes "O que o trouxe aqui nesse buraco de rato?";
	}
	else {
		mes "Ah...";
		mes "Você voltou. Você tem certeza que está pronto para tentar novamente?";
	}
	next;
	if (select("Ei, eu voltei aqui para virar um Gatuno!:Não, estou apenas dando uma olhada.") == 1) {
		if (job_thief_q == 0) {
			mes "[Guia de Gatuno]";
			mes "Heh, eu gosto de sua confiança. Mesmo assim, você sabe que se tornar um Gatuno não é tudo o que é rachado até ser.";
			next;
			mes "[Guia de Gatuno]";
			mes "Ainda...";
			mes "Você realmente quer ser um Gatuno?";
			next;
			switch(select("Sim.:Não, apenas gastando seu tempo.:Por que você virou um Gatuno?")) {
			case 1:
				mes "[Guia de Gatuno]";
				mes "Realmente...";
				break;
			case 2:
				mes "[Guia de Gatuno]";
				mes "É...";
				mes "Eu vi.";
				break;
			case 3:
				mes "[Guia de Gatuno]";
				mes "Eu...?";
				mes "Eu não tive escolha no momento. Era roubar ou passar fome. Mas eu não preciso te contar a história da minha vida.";
				break;
			}
			next;
			mes "[Guia de Gatuno]";
			mes "Então você quer se inscrever para ser um Gatuno ou não?";
			next;
			switch(select("Sim, eu quero.:Eu estou com muito medo de virar um Gatuno!")) {
			case 1:
				mes "[Guia de Gatuno]";
				mes "Correto, diga-me seu nome.";
				next;
				mes "[Guia de Gatuno]";
				mes "" + strcharinfo(0) + "?";
				mes "Que tipo de nome é " + strcharinfo(0) + "? De qualquer modo, espere um segundo.";
				job_thief_q = 1;
				next;
				mes "[Guia de Gatuno]";
				mes "Tudo certo, seu registro foi processado. Ok, você pode começar o teste se estiver pronto.";
				next;
				switch(select("Sim, estou pronto.:Não, ainda não estou pronto.")) {
				case 1:
					break;
				case 2:
					mes "[Guia de Gatuno]";
					mes "Não está pronto? Como você pode não estar pronto?!";
					close;
				}
				break;
			case 2:
				mes "[Guia de Gatuno]";
				mes "Está com muito medo?!?";
				mes "Hahahahahahah!";
				mes "Oh, por favor...!";
				mes "Isso é hilário!";
				close;
			}
		}
		else {
			mes "[Guia de Gatuno]";
			mes "Ok...";
			mes "Espere um segundo.";
			next;
		}
		mes "[Guia de Gatuno]";
		mes "Seu nome é...";
		mes strcharinfo(0) + "? Hmm, aonde está isso? Ah, aqui está. Deixe-me ver...";
		next;
		mes "[Guia de Gatuno]";
		if (getskilllv("NV_BASIC") < 9) {
			mes "Não é bonitinho? Eu posso ver que você é ambicioso, mas você precisa aprender as Habilidades Básicas antes de se tornar um Gatuno.";
			close;
		}
		mes "Tudo certo. Eu olhei sua ficha criminal, e você parece ter uma história muito interessante. Você deve ter o que é preciso para ser um Gatuno.";
		next;
		mes "[Guia de Gatuno]";
		mes "Porque eu sinto isso, eu agora decreto que você passou nessa entrevista. Bom trabalho!";
		job_thief_q = 2;
		setquest 1013;
		next;
		mes "[Guia de Gatuno]";
		mes "Agora, suas habilidades atuais serão testadas. Você conheçe alguma coisa sobre o teste?";
		next;
		switch(select("Sim, eu conheço.:Desculpe, eu não conheço.")) {
		case 1:
			mes "[Guia de Gatuno]";
			mes "É mesmo? Bem, isso tornará as coisas mais fáceis.";
			break;
		case 2:
			mes "[Guia de Gatuno]";
			mes "Vamos lá, deixe-me te informar então. Escute cautelosamente. Esse teste decide se você é digno de virar um Gatuno.";
			next;
			mes "[Guia de Gatuno]";
			mes "Você irá secretamente até a plantação do Shibu. Ele é o pior Mercador, em termos de caráter, em Morroc.";
			next;
			mes "[Guia de Gatuno]";
			mes "Vamos lá, para seu teste, você precisa roubar Cogumelos da plantação dele. Não se preocupe, ele merece ser roubado.";
			next;
			mes "[Guia de Gatuno]";
			mes "Enfim, você tem que juntar dois tipos de Cogumelos: ^0000FFCogumelo Venenoso^000000 e ^0000FFCogumelo Borrachento^000000.";
			next;
			mes "[Guia de Gatuno]";
			mes "Tenha cuidado, pois existem monstros na plantação que estão lá para proteger os Cogumelos. Então isso não será uma caminhada no parque.";
			next;
			mes "[Guia de Gatuno]";
			mes "Quando você voltar aqui depois de juntar os Cogumelos, você será pontuado de acordo com os Cogumelos que coletou.";
			next;
			mes "[Guia de Gatuno]";
			mes "Cada Cogumelo Venenoso vale 3 pontos, e você recebe 1 ponto por cada Cogumelo Borrachento. Você precisa de 25 pontos para passar no teste.";
			next;
			mes "[Guia de Gatuno]";
			mes "Vá lá fora e continue indo até o campo oriental das Pirâmides. E você verá um de nossos camaradas entre duas colunas.";
			next;
			mes "[Guia de Gatuno]";
			mes "Fale com aquele cara, e ele te levará para a plantação pela porta dos fundos.";
			next;
			mes "[Guia de Gatuno]";
			mes "Naquele campo, eu acho que as coordenadas são '^FF0000141, 125^000000.' Apenas digite ^3355FF/where^000000 no canto direito da caixa de chat para checar suas coordenadas atuais.";
		}
		next;
		mes "[Guia de Gatuno]";
		mes "Não se esqueça de fazer planos e se preparar antes de entrar na plantação. Ande o mais rápido que conseguir e tente não ser morto, ok?";
		close;
	}
	close;
}

moc_prydb1,42,133,2	script	Camarada	2_M_THIEFMASTER,{
	if (BaseJob == Job_Thief) {
		mes "[Brad]";
		mes "Nós não temos nenhum evento especial ainda. Venha outra hora quando tiver notícias, ok?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Camarada]";
		mes "Um...";
		mes "Você não se parece com um Gatuno.";
		next;
		mes "[Camarada]";
		mes "Que diabos você está fazendo aqui afinal?";
		close;
	}
	if (job_thief_q == 0 || job_thief_q == 1) {
		mes "[Camarada]";
		mes "Qual é o problema? Se você quer se tornar um Gatuno, fale com a garota ao meu lado.";
		close;
	}
	else if (job_thief_q == 2) {
		mes "[Camarada]";
		mes "Você não passou na entrevista? Então o que está esperando?";
		close;
	}
	else if (job_thief_q == 3) {
		mes "[Camarada]";
		mes "Ah, o mentor me falou de você. Então, deixe-me verificar seus Cogumelos...";
		if (countitem(1069) == 0 && countitem(1070) == 0) {
			next;
			mes "[Camarada]";
			mes "Que diabos...";
			mes "Você não tem nenhum Cogumelo! Volte lá e pegue-os. Caso contrário, você não passará no teste e não se tornará um Gatuno!";
			close;
		}
		next;
		.@thief_item1 = countitem(1069) * 3;
		.@thief_item2 = countitem(1070);
		.@total_thief = .@thief_item1 + .@thief_item2;
		.@money_thief = ((.@thief_item1 * 5) + (.@thief_item2 * 2)) + 200;
		mes "[Camarada]";
		if (countitem(1069) != 0) {
			mes "Primeiro, deixe-me checar quantos Cogumelos Venenoso você pegou.";
			mes "Huh, " + countitem(1069) + " deles.";
		}
		if (countitem(1070) != 0)
			mes "Agora vou checar seus Cogumelos Borrachento. Você pegou " + countitem(1070) + " deles.";
		next;
		mes "[Camarada]";
		mes "Então você trouxe";
		mes "um total de...";
		next;
		mes "[Camarada]";
		mes "Hmmm. " + .@total_thief + " graus, multiplicados pela velocidade da luz, dividido pelo integral de pi vezes o peso extra do valor absoluto dos políticos...";
		next;
		mes "[Camarada]";
		mes "Ok!";
		mes "Eu consegui.";
		next;
		mes "[Camarada]";
		if (.@total_thief > 25) {
			mes "Você conseguiu mais que 25 pontos! Impressionante!";
		}
		else if (.@total_thief == 25) {
			mes "Exatamente 25 pontos! Você conseguiu!";
		}
		else {
			mes "Definitivamente menos que os 25 pontos que você precisa para passar. Volte lá e pegue mais Cogumelos!";
			close;
		}
		next;
		mes "[Camarada]";
		mes strcharinfo(0) + "...";
		mes "Você passou no teste oficial de Gatuno. Agora você é um de nós.";
		if (countitem(1069) != 0) delitem 1069,countitem(1069); //Mushroom_Of_Thief_1
		if (countitem(1070) != 0) delitem 1070,countitem(1070); //Mushroom_Of_Thief_2
		callfunc "Job_Change",Job_Thief;
		callfunc "F_ClearJobVar";
		completequest 1013;
		next;
		mes "[Camarada]";
		mes "Parabéns por ter se tornado um Gatuno! Apartir de agora, seja um representante honorável do Clã de Gatunos.";
		next;
		mes "[Camarada]";
		mes "Se você trouxer desgraça para nosso clã, você será assassinado. Eu espero que você traga orgulho para nós.";
		next;
		mes "[Brad]";
		mes "*Ahem* Bem-vindo ao Clã, camarada! Eu sou Brad, e sou encarregado dos recursos humanos aqui.";
		next;
		mes "[Brad]";
		mes "Aqui está uma pequena ajuda para um iniciante como você. Gaste com o que você quiser. Tudo certo então, vejo você por ai~";
		Zeny += .@money_thief;
		close;
	}
}

moc_ruins,141,125,3	script	Sr. Irrelevante	4_M_01,{
	if (BaseJob == Job_Thief) {
		mes "[Sr. Irrelevante]";
		mes "Ah, eu vi que agora você é um Gatuno. Eu sempre soube que você iria se juntar a nós.";
		next;
		mes "[Sr. Irrelevante]";
		mes "Roubar de uma plantação de cogumelos é muito fácil pra você agora. Você deve aumentar suas habilidades e aprender nossa profissão.";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Sr. Irrelevante]";
		switch(rand(4)) {
		case 1:
			mes "Eu poderia beber uma boa bebida agora.";
			close;
		case 2:
			mes "Me dê seu dinheiro.";
			next;
			mes "[Sr. Irrelevante]";
			mes "Brincadeira, estou fora do horário.";
			close;
		case 3:
			mes "QUEM VOCÊ ESTÁ CHAMANDO DE PSICOPATA?!?!";
			close;
		case 4:
			mes "Eu não tenho nada pra te dizer. Importa-se de me deixar sozinho?";
			close;
		default:
			mes "Hoje parece um bom dia para ir nas pirâmides e caçar com alguns amigos.";
			close;
		}
	}
	if (job_thief_q == 3) {
		mes "[Sr. Irrelevante]";
		mes "Hahahahaha~!";
		mes "Você não passou no teste ainda? Ok, eu deixarei você entrar...";
		close2;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 2) {
		mes "[Sr. Irrelevante]";
		mes "Hmm...";
		mes "Você veio aqui por causa do teste, certo? Eu posso ver nos seus olhos que você sabe alguma coisa.";
		next;
		mes "[Sr. Irrelevante]";
		mes "Seu nome é " + strcharinfo(0) + "? Ah, está na lista. Tudo certo, eu deixarei você entrar na Plantação de Cogumelos, mas não posso garantir sua segurança...";
		close2;
		job_thief_q = 3;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 1) {
		mes "[Sr. Irrelevante]";
		mes "Tem um cheiro estranho vindo de... Você. Agora, por que razão?";
		close;
	}
	mes "[Sr. Irrelevante]";
	mes "Ei Aprendiz! Por que você não junta-se do Clã dos Gatunos? Vocês iniciantes são sempre bem-vindos para juntar-se a nós e nossa causa egoísta.";
	next;
	mes "[Sr. Irrelevante]";
	mes "Você pode obter mais informações na sala do subsolo da Pirâmide 1 BF.";
	close;
}

// Monstros
//============================================================
job_thief1,0,0,0,0	monster	Cogumelo Laranja	1182,180,0,0,0
job_thief1,0,0,0,0	monster	Fabre	1184,50,0,0,0
job_thief1,0,0,0,0	monster	Chonchon	1183,50,0,0,0
job_thief1,0,0,0,0	monster	Esporo	1014,30,0,0,0

//============================================================
// mapflag
//============================================================

