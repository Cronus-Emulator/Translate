//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Versão:                                                     ||
// - 2.7                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Tradução [Spell Master]                                     ||
//===============================================================||
// = Descrição:                                                  ||
// - Quest de Mudança de Classe para Ninja                       ||
//===============================================================//

alberta,30,65,3	script	Akagi	4_M_JOB_ASSASSIN,{
	if (Class == Job_Novice) {
		if (JobLevel == 10) {
			mes "[Akagi]";
			mes "Humm...";
			mes "Você deve ter vindo aqui sentindo que alguém estava esperando por você.";
			mes "Diga-me, você busca o caminho da paciência?";
			next;
			if(select("Não:Sim") == 1) {
				mes "[Akagi]";
				mes "Entendo.";
				mes "Cada um por si, suponho.";
				mes "Cuide-se.";
				close;
			}
			mes "[Akagi]";
			mes "Muito bem.";
			mes "Então, deixe-me levá-lo àquele local logo ali.";
			close2;
			switch(rand(3)) {
				case 1:
				warp "amatsu",170,229;
				end;
				case 2:
				warp "amatsu",216,188;
				end;
				default:
				warp "amatsu",178,176;
				end;
			}
		}
		else {
			mes "[Akagi]";
			mes "Hm? Não posso te dar nenhum serviço até que se torne um pouco mais forte...";
			close;
		}
	}
	else {
		mes "[Akagi]";
		mes "Hmm... Você e eu...";
		mes "Somos bastante parecidos em termos de habilidade de combate.";
		mes "Talvez possamos treinar juntos algum dia.";
		close;
	}
}

que_ng,30,65,3	script	Cougar#nq	4_M_JOB_ASSASSIN,{
	if (Upper == 2) {
		mes "[Cougar]";
		mes "Eu... Eu nunca vi uma criancinha tão poderosa quanto você!";
		mes "S-se afaste, sua aberração da natureza!";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10) {
			mes "[Cougar]";
			mes "Como tem passado?";
			mes "Treine duro: você quer ser capaz de desaparecer sem deixar rastros.";
			mes "Se você puder fazer isso, terá o respeito de qualquer Ninja.";
			close;
		}
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]";
			mes "Com licença. O-olá?";
			next;
			mes "[Cougar]";
			mes "...............................";
			mes "Como fez isso?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Fiz o quê?";
			mes "Eu não fiz nada, eu acho que não...";
			next;
			mes "[Cougar]";
			mes "C-como você é capaz de me vez?";
			mes "Era suposto que eu estivesse invisível a olho nu!";
			mes "Ah, agora entendi.";
			mes "Gato Selvagem deve ter te mandado para me matar!";
			mes "Não cairei em seus truques!";
			mes "Morra!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "E-espere!";
			mes "Eu nem mesmo sei quem sei quem Gato Selvagem é!";
			mes "Acalme-se, não há motivo para ficar violento!";
			next;
			mes "[Cougar]";
			mes "Quê...? Como se esquivou de todos os meus ataques?";
			mes "Você tem algum talento, eu admito.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "...............................";
			mes "Eu vim aqui esperando mudar minha classe para Ninja.";
			next;
			mes "[Cougar]";
			mes "...Oh. Isso é tudo?";
			mes "Hmm, você tem grande potencial, mas não posso te ajudar agora.";
			mes "Tenho inimigos demais, e não posso baixar a guarda nem por um segundo.";
			next;
			mes "[Cougar]";
			mes "Aquele Gato Selvagem é completamente implacável...!";
			mes "Ele poderia atacar a qualquer hora!";
			mes "Ele faria qualquer coisa para alcançar a vitóra sobre seus inimigos!";
			next;
			mes "[Cougar]";
			mes "Espere, espere, acabei de pensar em algo.";
			mes "Talvez você possa me ajudar.";
			mes "Faça o que peço, e irei ensiná-lo algumas de minhas habilidades se você realmente quiser se tornar um Ninja.";
			next;
			if(select("Com certeza!:Não, obrigado.") == 2) {
				mes "[Cougar]";
				mes "Hm? Bem, tudo bem.";
				mes "Entretanto, não entendo porque não podemos nos ajudar neste pequeno dilema...";
				close;
			}
			mes "[Cougar]";
			mes "Ótimo!";
			mes "Agora, eu queria perguntar ao Gato Selvagem se ele concordaria com uma trégua temporária.";
			mes "Estou ciente que ambos estamos sem armas, então deveríamos nos equipar melhor antes.";
			next;
			mes "[Cougar]";
			mes "Por favor, pegue esta carta e entregue-a a Gato Selvagem, em Einbroch.";
			mes "Ele é um mestre dos disfarces, então mantenha um olhar atento a ele.";
			mes "Ah, e procure-o em um local alto.";
			next;
			mes "[Cougar]";
			mes "Sim, Gato Selvagem sempre teve uma queda por se esconder em lugares altos.";
			mes "De qualquer maneira, depois de entregar-lhe a carta, volte e me diga sua resposta.";
			NINJ_Q = 1;
			setquest 6015;
			close;
		}
		else if (NINJ_Q == 1) {
			mes "[Cougar]";
			mes "Mesmo que esta tarefa não seja tão urgente.";
			mes "Por favor, corra para Einbroch e entregue minha carta para Gato Selvagem.";
			close;
		}
		else if (NINJ_Q == 2) {
			mes "[Cougar]";
			mes "Você entregou aquela carta para Gato Selvagem?";
			mes "Eu ainda preciso saber a resposta dele para minha proposta de trégua.";
			mes "De qualquer maneira, veja se consegue forçá-lo a isso.";
			close;
		}
		else if (NINJ_Q == 3) {
			mes "[Cougar]";
			mes "Ah, você voltou.";
			mes "Então, Gato Selvagem mandou você de volta com a resposta dele?";
			mes "Ótimo, ótimo, por favor, leia-a para mim.";
			next;
			mes "[Cougar]";
			mes "Quê...?!";
			mes "Como ele poderia rejeitar minha proposta de trégua?!";
			mes "Isto só pode significar que ele fez outra Kunai.";
			mes "Poxa!";
			mes "Tenho que alcançá-lo ou estarei arruinado!";
			next;
			mes "[Cougar]";
			mes "Ouça, você tem que me ajudar de novo!";
			mes "Preciso que você me traga alguns materiais para que eu possa criar minha própria Kunai para lutar com Gato Selvagem.";
			mes "Então, cumprirei nosso trato e mudarei sua classe para Ninja.";
			next;
			mes "[Cougar]";
			mes "Tudo o que precisa me trazer é";
			mes "^3355FF5 Cyfars^000000 e";
			mes "^3355FF1 Fracon^000000.";
			mes "Por favor, consiga-os o mais rápido que puder!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Huh?";
			mes "Que engraçado, Gato Selvagem me pediu para conseguir os mesmos materiais.";
			next;
			mes "[Cougar]";
			mes "Maldição!";
			mes "Então isso significa...";
			mes "Que você ajudou Joe em criar sua Kunai!";
			mes "Não!";
			mes "Eu deveria ter pensado nisso antes!";
			mes "Bem, é tarde demais agora.";
			mes "Apenas apresse-se!";
			NINJ_Q = 4;
			changequest 6017,6018;
			close;
		}
		else if (NINJ_Q == 4) {
			if (countitem(7053) < 5 || countitem(1010) < 1) {
				mes "[Cougar]";
				mes "Se apresse e traga";
				mes "^3355FF5 Cyfars^000000 e";
				mes "^3355FF1 Fracon^000000";
				mes "para mim, então poderei criar minha própria Kunai para usar contra Gato Selvagem!";
				close;
			}
			if (SkillPoint != 0) {
				mes "[Cougar]";
				mes "Whoa, whoa...";
				mes "Você ainda tem alguns Pontos de Habilidade restantes.";
				mes "É melhor gastar todos eles antes de mudar de classe, certo?";
				close;
			}
			mes "[Cougar]";
			mes "Ah, você está de volta com tudo que preciso.";
			mes "Voltou antes do que eu esperava, eh?";
			mes "Ótimo, como prometido, te transformarei em um Ninja.";
			next;
			mes "[Cougar]";
			mes "Permita que eu me apresente formalmente.";
			mes "Eu sou o Mestre Ninja Cougar do Grupo Ninja Touga, e estou encarregado do grupo de busca para encontrar Senhor Kazma.";
			next;
			mes "[Cougar]";
			mes "Senhor Kazma é o chefe do meu vilarejo, mas ele fugiu.";
			mes "Isto resultou em um conflito interno entre o Grupo Ninja.";
			mes "As coisas estão bem instáveis agora...";
			next;
			mes "[Cougar]";
			mes "Inicialmente, não queria te aceitar como um Ninja, por causa dessa situação complicada.";
			mes "Contudo, você provou que é realmente digno de se juntar ao grupo dos Ninjas.";
			next;
			mes "[Cougar]";
			mes "De acordo com a carta dele, até mesmo Joe reconhece seu valor.";
			mes "Basta lembrar que, como um Ninja, sua missão é sua maior prioridade.";
			mes "Mas não deixe os objetivos da missão tornem-se mais importantes que sua consciência.";
			next;
			mes "[Cougar]";
			mes "''Sigilo acima de tudo.''";
			mes "Para manter nossos segredos nas sombras, você pode somente comprar ou vender armas Ninja em comerciantes autorizados.";
			mes "Por favor, lembre-se disso.";
			next;
			mes "[Cougar]";
			mes "A partir de hoje, você é um membro do esplêndido Grupo Ninja Touga.";
			mes "Seja tão ágil como o vento, e tão silencioso como a o cair das sombras.";
			callfunc "Job_Change",Job_Ninja;
			NINJ_Q = 5;
			delitem 7053,5;  // Cyfar
			delitem 1010,1;  // Phracon
			getitem 13010,1; // Asura
			completequest 6018;
			close;
		}
		else {
			mes "[Cougar]";
			mes "Como tem passado?";
			mes "Treine duro: Você quer ser capaz de desaparecer sem deixar rastros.";
			mes "Se você puder fazer isso, terá o respeito de qualquer Ninja.";
			close;
		}
	}
	else {
		if (BaseClass == Job_Ninja) {
			mes "[Cougar]";
			mes "Como tem passado?";
			mes "Treine duro: Você quer ser capaz de desaparecer sem deixar rastros.";
			mes "Se você puder fazer isso, terá o respeito de qualquer Ninja.";
			close;
		}
		else {
			mes "[Cougar]";
			mes "Quê...? Como você foi capaz de me encontrar nas sombras?!";
			mes "Você deve ser mais que um aventureiro comum, eh?";
			close;
		}
	}
}

einbroch,184,194,3	script	Homem Suspeito#nq	4_M_SITDOWN,{
	if (NINJ_Q == 1) {
		mes "[Homem Suspeito]";
		mes "Eu viajei para muitos países, mas nunca estive em um prédio tão alto como a Torre de Einbroch.";
		mes "Todos os prédios na minha cidade natal são minúsculos em comparação...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Oh, você é de Amatsu?";
		mes "Estou procurando alguém de lá que é chamado Gato Selvagem.";
		next;
		mes "[Homem Suspeito]";
		mes "Não... Não, na verdade sou de Izlude, e estou aqui em Einbroch somente por alguns minérios.";
		mes "Diga-me, por que está procurando por Gato Selvagem?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Bem, preciso entregar esta carta para ele e obter sua resposta para que eu possa me tornar um Ninja.";
		next;
		mes "[Homem Suspeito]";
		mes "Sério?";
		mes "Pensando bem, eu acho que já o encontrei uma ou duas vezes nesta cidade.";
		mes "No entanto, ele prefere ser chamado de \"Leopardo Vermelho\", ao invés de \"Gato Selvagem\".";
		next;
		mes "[Homem Suspeito]";
		mes "Eu realmente quero te ajudar a encontrá-lo, mas primeiro eu preciso encontrar alguns minérios que estou procurando.";
		mes "Se não se importar, poderia me ajudar?";
		mes "Depois posso te ajudar a encontrar Joe.";
		next;
		if(select("Não se preocupe, eu o encontrarei sozinho.:Claro, eu te ajudarei.") == 1) {
			mes "[Homem Suspeito]";
			mes "Tem certeza...?";
			mes "Leopardo Vermelho é realmente um mestre dos disfarces.";
			mes "Você precisará de toda ajudar que puder para encontrá-lo...";
			close;
		}
		mes "[Homem Suspeito]";
		mes "Ótimo, estou feliz por ouvir isso.";
		mes "Por favor, ajude-me a encontrar";
		mes "^3355FF5 Cyfars^000000 e";
		mes "^3355FF1 Fracon^000000.";
		NINJ_Q = 2;
		changequest 6015,6016;
		close;
	}
	else if (NINJ_Q == 2) {
		if (countitem(7053) < 5 || countitem(1010) < 1) {
			mes "[Homem Suspeito]";
			mes "Por favor, traga ";
			mes "^3355FF5 Cyfars^000000 e";
			mes "^3355FF1 Fracon^000000";
			mes "para mim o mais rápido que puder.";
			mes "Então, eu posso te ajudar a encontrar Leopardo Vermelho.";
			close;
		}
		mes "[Homem Suspeito]";
		mes "Bom, muito bom.";
		mes "Você trouxe os minerais...";
		mes "Agora, é minha vez de te ajudar.";
		mes "Aqui, deixe-me ver essa carta.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "?????!!";
		next;
		mes "[Homem Suspeito]";
		mes "Por quê?";
		mes "Você não trouxe essa carta de Cougar para mim?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Você é.. Você é Gato Selvagem?";
		next;
		mes "[Homem Suspeito]";
		mes "...Sim, mas prefiro ser chamado Leopardo Vermelho.";
		mes "Cougar o mandou a mim, certo?";
		mes "Ele é o único que me chama assim.";
		mes "Então você quer ser um ninja, eh?";
		mes "Hmm, está bem.";
		next;
		mes "[Leopardo Vermelho]";
		mes "Se você quiser ser um Ninja, deve sempre ser cuidadoso com o que vê e em o quem confia.";
		mes "Não se esqueça de que, se os seus segredos forem descobertos, então você está acabado como Ninja.";
		next;
		mes "[Leopardo Vermelho]";
		mes "Lembre-se de se mover rapidamente, e de sempre desaparecer sem deixar rastros.";
		mes "Permanecer oculto nas sombras é realmente nosso poder supremo.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Entendo... ...........";
		next;
		mes "[Leopardo Vermelho]";
		mes "Por agora, deixe-me ler esta carta.";
		mes "Vamos ver... Hm.";
		mes "Pensei que Cougar queria me desafiar de novo, mas na verdade ele quer uma trégua temporária? Hah!";
		next;
		mes "[Leopardo Vermelho]";
		mes "Obrigado por sua ajuda, agora que tenho minérios, preciso construir uma Kunai!";
		mes "Hahaha! Não concordarei com uma trégua quando eu tenho a vantagem!";
		next;
		mes "[Leopardo Vermelho]";
		mes "De qualquer maneira, deixe-me escrever minha resposta para ele.";
		mes "Também vou escrever uma recomendação para você...";
		mes "Acho que você vai se tornar um excelente Ninja, mesmo que eu tenha acabado de te enganar.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "[Leopardo Vermelho]";
		mes "Aqui está.";
		mes "Por favor, leve esta carta para Cougar.";
		mes "Demoraria um pouco para voltar para Amatsu, então deixe-me mandá-lo diretamente para lá...";
		delitem 1010,1;	// Phracon
		delitem 7053,5;	// Cyfar
		NINJ_Q = 3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 3) {
		mes "[Leopardo Vermelho]";
		mes "Eh?";
		mes "Não sei o que aconteceu, mas parece que você ainda não entregou minha resposta para Cougar.";
		mes "Devo enviar-lhe diretamente para Amatsu agora?";
		next;
		if(select("Não, obrigado.:Sim, por favor.") == 1) {
			mes "[Leopardo Vermelho]";
			mes "Certo.";
			mes "Bem, eu estava só tentando poupar algum tempo seu.";
			close;
		}
		mes "[Leopardo Vermelho]";
		mes "Okay, então. Adeus por enquanto.";
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 4) {
		mes "[Leopardo Vermelho]";
		mes "Cougar te pediu para obter alguns materiais também?";
		mes "Oh, bem, suponho que não posso culpá-lo.";
		mes "Aliás, devo ser capaz de vencê-lo em uma luta justa.";
		close;
	}
	else if (NINJ_Q == 5 && BaseClass == Job_Ninja) {
		mes "[Leopardo Vermelho]";
		mes "Oh, você é um Ninja.";
		mes "Espero que continue a treinar e maximizar todas as habilidades de Ninja que puder.";
		mes "Sempre se lembre de misturar-se com as sombras.";
		close;
	}
	else {
		mes "[Turista]";
		mes "Eu viajei para muitos países, mas nunca estive em um prédio tão alto como a Torre de Einbroch.";
		mes "Todos os prédios na minha cidade natal são minúsculos em comparação...";
		close;
	}
}
