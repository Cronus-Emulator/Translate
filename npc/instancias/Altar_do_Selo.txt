//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Criação:                                                    ||
// - eA/Herc/Cronus                                              ||
//===============================================================||
// = Versão:                                                     ||
// - 2.3                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Tradução [Revisões Cronus]			                         ||
// - Ajustes [SlexFire]       			                         ||
//===============================================================||
// = Descrição:                                                  ||
// - Instância do Altar do Selo                                  ||
//===============================================================//

1@cata	mapflag	src4instance
2@cata	mapflag	src4instance
//============================================================

// Entrada da catacumba selada
//============================================================
monk_test,309,146,3	script	Frei Patrick#edq	4_M_OLDFRIAR,{
	cutin "ins_cata_pri_n",2;
	mes "[Frei Patrick]";
	mes "A paz do mundo jamais pode durar para sempre... As mãos do Mal estão chegando ao mundo novamente...";
	next;
	mes "[Frei Patrick]";
	mes "O que te leva a este lugar?";
	next;
	if (countitem(6004) > 0) {
		.@doll = 1;
		.@selection = select("Que lugar é este?:Quero entrar.:Sobre o Boneco de Baphomet Amaldiçoado.:Cancelar.");
	}
	else {
		.@selection = select("Que lugar é este?:Quero entrar.:Cancelar.");
	}
	switch(.@selection) {
	case 1:
		mes "[Frei Patrick]";
		mes "Huh... Você não sabe? Este é o Monastério de St. Capitolina onde os irmãos treinam e oram para virarem monjes.";
		next;
		mes "[Frei Patrick]";
		mes "E esse lugar é... Como eu posso falar... Sim. É o túmulo do Demônio. Túmulo...";
		next;
		mes "[Frei Patrick]";
		mes "É o lugar onde o grande Demônio que demoliu esse mundo uma vez se encontra dormindo.";
		next;
		switch(select("Me conte mais.:Terminar a conversa.")) {
		case 1:
			cutin "ins_cata_pri_n",2;
			mes "[Frei Patrick]";
			mes "Baphomet... é o nome do Demônio... Eu acho que você já tenha escutado o nome dele.";
			next;
			mes "[Frei Patrick]";
			mes "Vários homens bravos e irmãos treinaram nesse monastério...";
			next;
			mes "[Frei Patrick]";
			mes "Abaixo desse túmulo na sua frente... Baphomet se encontra selado.";
			next;
			mes "[Frei Patrick]";
			mes "Porém... Como foi dito, o Imperador Morroc está manipulando suas magias poderosas para afetar toda vida nesse continente.";
			next;
			mes "[Frei Patrick]";
			mes "Baphomet também... tem despertado e está se preparando para sua ressurreição, dentro desse mundo, com o enfraquecimento do selo devido ao poder do Imperador Morroc...";
			next;
			mes "[Frei Patrick]";
			mes "Agora... Estou procurando por pessoas bravas o suficiente para selar Baphomet em seu santuário... como nós fizemos muitos anos atrás...";
			next;
			mes "[Frei Patrick]";
			mes "Todos que lutam pelo bem, devem saber, que o mal ameaça a conquistar esse mundo...";
			next;
			mes "[Frei Patrick]";
			mes "Treine mais e use suas habilidades para proteger o mundo da eminente presença do mal...";
			break;
		case 2:
			cutin "ins_cata_pri_n",2;
			mes "[Frei Patrick]";
			mes "Todos que lutam pelo bem, devem saber, que o mal ameaça conquistar esse mundo...";
			next;
			mes "[Frei Patrick]";
			mes "Treine mais e use suas habilidades para proteger o mundo da eminente presença do mal...";
			break;
		}
		break;
	case 2:
		if (BaseLevel >= 75) {

			.@party_id = getcharid(1);

			mes "[Frei Patrick]";
			mes "Está certo que você irá ao santuário e selar Baphomet novamente?";
			next;

			// 12 hour cooldown
			.@ins_bapho_check = questprogress(3040,PLAYTIME);
			// Two hour play limit
			//.@ins_bapho_check2 = questprogress(3045,PLAYTIME);

			if (!.@ins_bapho_check) {
				if (getpartyleader(.@party_id,2) == getcharid(0) && instance_check_party(.@party_id,2,75)) {
					mes "[Frei Patrick]";
					mes "O nome do grupo é "+getpartyname(.@party_id)+"...";
					mes "O nome do líder é "+strcharinfo(0)+"...";
					.@instance = instance_create("Sealed Catacomb",.@party_id);
					if (.@instance < 0) {
						mes "Umm... Mas parece que temos um problema aqui... Irei checar rapidamente. Por favor aguarde.";
					}
					else {
						for (.@i = 1; .@i <= 2; .@i++) {
							if( instance_attachmap(.@i + "@cata", .@instance) == "" ) {
								mes "Falha ao vincular "+.@i+"@cata como um mapa!";
								instance_destroy(.@instance);
								close;
							}
						}

						instance_set_timeout 7200,300,.@instance;
						instance_init(.@instance);

						mes "Ok... Irei ajustar o selo do santuário para você e seu grupo possam entrar..";
						next;
						mes "[Frei Patrick]";
						mes "Você verá um sinal quando o selo for destruído. Aguarde até o sinal aparecer...";
						next;
						mes "[Frei Patrick]";
						mes "Quando você ver o sinal, coloque suas mãos sobre o túmulo... E então você pode empurrá-lo.";
						next;
						mes "[Frei Patrick]";
						mes "Uma coisa que você deve lembrar é... Todos aqueles que entrarem no santuário serão amaldiçoados por Baphomet e não poderam entrar ou sair enquando estiverem amaldioçoados.";
						next;
						mes "[Frei Patrick]";
						mes "E mais uma coisa... Nessa área amaldiçoada, certas habilidades, que são protegidas por uma força exterior, são proibidas pelo efeito do selo.";
						next;
						mes "[Frei Patrick]";
						mes "Por exemplo, habilidades como ^0000FFEscudo Mágico e Assumptio^000000... Então é melhor você estar preparado entes de entrar no santuário.";
					}
				}
				else {
					mes "[Frei Patrick]";
					mes "Umm... Eu reconheço usa coragem, porém... Não posso permitir qualquer um entrar nesse lugar. Eu só permito o líder do grupo a entrar primeiro.";
					next;
					mes "[Frei Patrick]";
					mes "Uma vez que o líder grupo foi permitido, o resto do grupo pode entrar. Isto é uma regra deste monastério, por favor a entenda.";
				}
			}
			else if (.@ins_bapho_check == 1) {
				mes "[Frei Patrick]";
				mes "Parece que você entrou no santuário recentemente... Você não pode entrar novamente pois a Maldição de Baphomet ainda continua. A maldição de Baphomet só desaparece quando um certo período de tempo se passa.";
			}
			else if (.@ins_bapho_check == 2) {
				mes "[Frei Patrick]";
				mes "Umm... Parece que a Maldição de Baphomet se enfraqueceu. Eu posso retira-la agora.";
				next;
				mes "[Frei Patrick]";
				mes "Haaaaaaap... Hocus Pocus Wingardium Abracadabra!!!!!";
				next;
				erasequest 3040;
				if (questprogress(3041)) erasequest 3041;
				if (questprogress(3045)) erasequest 3045;
				mes "[Frei Patrick]";
				mes "Huu... Acabou. Agora que retirei a Maldição de Baphomet, você pode entrar novamente.";
			}
		}
		else {
			mes "[Frei Patrick]";
			mes "Umm... Você precisa treinar mais para entrar nesse lugar perigoso... Você tem que estar no mínimo no nível 75 para poder entrar aqui.";
			next;
			mes "[Frei Patrick]";
			mes "Treine mais e volte novamente.";
		}
		break;
	case 3:
		if (.@doll == 1) {
			mes "[Frei Patrick]";
			mes "O que é... esse boneco que está segurando?... Deixe me ver.";
			next;
			mes "[Frei Patrick]";
			mes "... ... ...";
			next;
			mes "[Frei Patrick]";
			mes "Correto... Posso sentir os sentimentos de Baphomet... O que eu farei com esse boneco?";
			next;
			mes "[Frei Patrick]";
			mes "É muito perigoso que alguém perca esse objeto... Conheço alguem que pode te ajudar... Deseja falar com ele?";
			next;
			mes "[Frei Patrick]";
			mes "Fale com ^0000FFRust Blackhand^000000 que está proximo a uma contrução do monastério... Ele pode fazer com que este boneco seja útil para você.";
			setquest 3042;
			break;
		}
	case 4:
		mes "[Frei Patrick]";
		mes "Todos que lutam pelo bem, devem saber, que o mal ameaça a conquistar esse mundo...";
		next;
		mes "[Frei Patrick]";
		mes "Treine mais e use suas habilidades para proteger o mundo da eminente presença do mal...";
		break;
	}
	close2;
	cutin "",255;
	end;
}

monk_test,306,151,3	script	Grave of Baphomet#edq	HIDDEN_NPC,{
	if (countitem(6002)) delitem 6002,countitem(6002); //Token_Of_Apostle
	mes "Esse túmulo tem uma imagem de um grande demônio com enormes chifres. Ela desperta um sentimento ameaçador.";
	next;
	if(select("Tocar na pedra.:Voltar.") == 2)
		close;

	// 12 hour cooldown
	.@ins_bapho_check = questprogress(3040,PLAYTIME);
	// Two hour play limit
	//.@ins_bapho_check2 = questprogress(3045,PLAYTIME);

	if (!.@ins_bapho_check) {
		if (has_instance("1@cata") == "") {
			mes "É frio ao tocar. Mais nada acontece.";
			close;
		} else {
			mapannounce "monk_test","[" + strcharinfo(0) + "] membro do grupo [" + getpartyname(.@party_id) + "] entrou no Santuário Selado.",bc_map,"0x00ff99";
			setquest 3040;
			warp "1@cata",100,224;
			end;
		}
	} else if (.@ins_bapho_check == 1) {
		mes "[Frei Patrick]";
		mes "Parece que você entrou no santuário recentemente... Você não pode entrar novamente pois a Maldição de Baphomet ainda continua.";
		mes "A Maldição de Baphomet desparece após de um certo tempo após sua entrada.";
		close;
	} else if (.@ins_bapho_check == 2) {
		mes "[Frei Patrick]";
		mes "Humm... Parece que a maldição de Baphomet está enfraquecendo. Irei retirá-la completamente.";
		next;
		mes "[Frei Patrick]";
		mes "Haaaaaaap... Wingardium Leviosa Expecto Patronum !!!!!";
		specialeffect2 EF_HOLYHIT;
		erasequest 3040;
		if (questprogress(3041)) erasequest 3041;
		if (questprogress(3045)) erasequest 3045;
		next;
		mes "[Frei Patrick]";
		mes "Huu... Acabou. Agora que retirei todas as maldições sobre você. Você pode entrar novamente.";
		close;
	}
}

prt_monk,261,91,3	script	Rust Blackhand#edq	4_M_DWARF,{
	.@new_maje = questprogress(3043);
	mes "[Rust Blackhand]";
	mes "Quem é você? O que deseja?";
	next;
	mes "[Rust Blackhand]";
	mes "Você não é um monje, o que quer de mim?";
	next;
	if (.@new_maje == 1) {
		mes "[Rust Blackhand]";
		mes "Você trouxe todos os materiais?";
		next;
		if (countitem(6004) && countitem(2256) && (countitem(7799) > 29) && (countitem(7798) > 49) && (Zeny >= 990000)) {
			mes "[Rust Blackhand]";
			mes "kkk... Irei preparar os chifres. Tem como esperar um pouco?";
			next;
			mes "[Rust Blackhand]";
			mes "Humm... Faz tempo que vejo esses chifres... kkk... Deixe-me começar...";
			next;
			mes "...";
			next;
			mes "... ...";
			next;
			mes "... ... ...";
			next;
			delitem 6004,1; //Bapho_Doll
			delitem 2256,1; //Magestic_Goat
			delitem 7799,30; //Dark_Crystal
			delitem 7798,50; //Dark_Debris
			Zeny -= 990000;
			completequest 3043;
			getitem 5374,1; //L_Magestic_Goat
			mes "[Rust Blackhand]";
			mes "Está pronto. Você já deve estar excitado. Eu entendo...";
			next;
			mes "[Rust Blackhand]";
			mes "Não aceito reclamações sobre meu serviço. Então vá...";
			close;
		}
		else {
			mes "[Rust Blackhand]";
			mes "Huu... Você não entendeu o que eu disse. Você não pode fazer nada com esses ingredientes.";
			next;
			mes "[Rust Blackhand]";
			mes "Irei falar sobre os materiais novamente. São eles:";
			next;
			mes "[Rust Blackhand]";
			mes "^0000FFBoneco de Baphomet Amaldiçoado, Chifres Magestosos, 30 Cristais da Escuridão, 50 Fragmentos da Escuridão^000000, e o mais importante, o custo da produção é de ^0000FF 990000^000000 Zeny.";
			close;
		}
	}
	else if (!.@new_maje && countitem(6004) > 0) {
		switch(select("Sobre o Boneco de Baphomet Amaldiçoado:Terminar conversa.")) {
		case 1:
			mes "[Rust Blackhand]";
			mes "Como?... Hmmm... Você conseguiu o boneco?...";
			next;
			mes "[Rust Blackhand]";
			mes "Vamos ver... Foi o Patrick que te trouxe até aqui?";
			next;
			mes "[Rust Blackhand]";
			mes "Certo... Vou fazer com que esse boneco seja algo de ultilidade. O que poderei fazer?";
			next;
			mes "[Rust Blackhand]";
			mes "Posso fazer fortes e largos chifres com toda a força de Baphomet contida nele.";
			next;
			mes "[Rust Blackhand]";
			mes "Seu nome é ^4d4dffChifres Magestosos Gigantes^000000. Os chifres Majestosos normais não são nada, comparados à esses.";
			next;
			mes "[Rust Blackhand]";
			mes "O Boneco de Baphomet Amaldiçoado é a peça mais importante... Poderei fazer isso se você quiser. Deseja que eu o faça?";
			next;
			switch(select("Quero sim.:Não preciso disso!")) {
			case 1:
				mes "[Rust Blackhand]";
				mes "kkk... Sim, homens sabem o que fazem quando a oportunidade chega. Irei lhe falar os ingredientes. Não esqueça e traga todos eles.";
				next;
				mes "[Rust Blackhand]";
				mes "^0000FFBoneco de Baphomet Amaldiçoado, Chifres Magestosos, 30 Cristais da Escuridão, 50 Fragmentos da Escuridão^000000, e o mais importante, o custo da produção é de ^0000FF 990000^000000 Zeny.";
				next;
				mes "[Rust Blackhand]";
				mes "Você pode conseguir os chifres Majestosos dos Baphomets enfraquecidos do labirinto da floresta. E os cristais você consegue com as Sombras de Morroc.";
				next;
				mes "[Rust Blackhand]";
				mes "Estou certo que você poderá conseguir os ingredientes, até porque você conseguiu selar o verdadeiro Baphomet. Não é? kkk...";
				next;
				mes "[Rust Blackhand]";
				mes "Você nunca saberá como é bom isso até conseguir um. Se você souber, vá e pegue os ingredientes.";
				if (questprogress(3042))
					erasequest 3042;
				setquest 3043;
				close;
			case 2:
				mes "[Rust Blackhand]";
				mes "Huh... Não mesmo? Faça o que quiser... Você quer realmente perder essa oportunidade?";
				next;
				mes "[Rust Blackhand]";
				mes "Tut, tut... Eu não ligo se esse boneco ameaça sua vida todo o tempo!";
				close;
			}
		case 2:
			mes "[Rust Blackhand]";
			mes "Que menino chato é esse?... huh...";
			close;
		}
	}
	else if (!.@new_maje && !countitem(6004)) {
		mes "[Rust Blackhand]";
		mes "Se não tiver nada a fazer, vá embora! Como você pode ver, eu só faço equipamentos para os irmãos do monastério.";
		close;
	}
	else if (.@new_maje == 2 && countitem(6004)) {
		switch(select("Sobre o Boneco de Baphomet Amaldiçoado:Terminar conversa.")) {
		case 1:
			mes "[Rust Blackhand]";
			mes "Que?... Você denovo? O que você quer desta vez?";
			next;
			mes "[Rust Blackhand]";
			mes "Como assim? Você conseguiu outro boneco do perverso demônio? Umm... Você é bem melhor do que eu pensava...";
			next;
			mes "[Rust Blackhand]";
			mes "Está certo... Irei lhe ajudar novamente.";
			next;
			mes "[Rust Blackhand]";
			mes "Se você quiser tranformar o seu boneco num^4d4dffChifres Magestosos Gigantes^000000 novamente, eu poderei fazer outro.";
			next;
			mes "[Rust Blackhand]";
			mes "Irei lhe falar os ingredientes novamente. Então, você deseja fazer?";
			next;
			switch(select("Quero sim.:Não preciso disso!")) {
			case 1:
				mes "[Rust Blackhand]";
				mes "kkk... Sim, homens sabem o que fazem quando a oportunidade chega. Irei lhe falar os ingredientes. Não esqueça, e traga eles todos.";
				next;
				mes "[Rust Blackhand]";
				mes "^0000FFBoneco de Baphomet Amaldiçoado, Chifres Magestosos, 30 Cristais da Escuridão, 50 Fragmentos da Escuridão^000000, e o mais importante, o custo da produção é de ^0000FF 990000^000000 Zeny.";
				next;
				mes "[Rust Blackhand]";
				mes "Você pode conseguir os chifres Majestosos dos Baphomets enfraquecidos do labirinto da floresta. E os cristais você consegue com as Sombras de Morroc.";
				next;
				mes "[Rust Blackhand]";
				mes "Estou certo que você poderá conseguir os ingredientes até porque você conseguiu selar o verdadeiro Baphomet. Não é? kkk...";
				next;
				mes "[Rust Blackhand]";
				mes "Você nunca saberá como é bom isso até conseguir um. Se você souber, vá e pegue os ingredientes.";
				if (questprogress(3042))
					erasequest 3042;
				erasequest 3043;
				setquest 3043;
				close;
			case 2:
				mes "[Rust Blackhand]";
				mes "Huh... Não mesmo? Faça o que quiser... Você quer realmente perder essa oportunidade?";
				next;
				mes "[Rust Blackhand]";
				mes "Tut, tut... Eu não ligo se esse boneco ameaça sua vida todo o tempo!";
				close;
			}
		case 2:
			mes "[Rust Blackhand]";
			mes "Que menino chato é esse?... huh...";
			close;
		}
	}
	else if (.@new_maje == 2 && !countitem(6004)) {
		mes "[Rust Blackhand]";
		mes "O está fazendo aqui? Se não quer os ^4d4dffChifres Magestosos Giganetes^000000, saia daqui";
		close;
	}
}

// Interior do santuário selado
//============================================================
1@cata,1,2,0	script	ins_baphomet_lotto	-1,{
OnInstanceInit:
	// Pick one "true" Gravestone and 12 "false" Gravestones.
	.@true = rand(1,13);
	for (.@i = 1; .@i<13; ++.@i) {
		disablenpc instance_npcname("Túmulo#1F_1"+((.@i == .@true)?"F":"T"));
	}
	disablenpc instance_npcname("ins_baphomet_lotto");
	end;
}

1@cata,3,2,0	script	ins_baphomet_lotto2	-1,{
OnEnable:
	for (.@i = 1; .@i < 12; ++.@i)
		enablenpc instance_npcname("Tocha#"+.@i);
	end;
}

1@cata,4,2,0	script	ins_baphomet_lotto3	-1,{
OnEnable:
	.@label$ = instance_npcname("ins_baphomet_lotto3")+"::OnMyMobDead";
	.@map$ = instance_mapname("1@cata");
	monster .@map$,0,0,"Apóstolo de Baphomet",1869,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1291,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1869,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1291,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1869,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1291,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1869,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1132,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1117,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1132,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1117,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1132,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1291,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1117,1,.@label$;
	monster .@map$,0,0,"Apóstolo de Baphomet",1869,1,.@label$;
	end;

OnDisable:
	killmonster instance_mapname("1@cata"),instance_npcname("ins_baphomet_lotto3")+"::OnMyMobDead";
	end;

OnMyMobDead:
	.@map$ = instance_mapname("1@cata");
	if (mobcount(.@map$,instance_npcname("ins_baphomet_lotto3")+"::OnMyMobDead") < 1) {
		mapannounce .@map$, "Todos os Apóstolos de Baphomet estão mortos!",bc_map,"0x00ff99";
	}
	getitem 6002,1; //Token_Of_Apostle
	end;
}

// Alma do herói perto do túmulo norte
//============================================================
1@cata,141,221,0	script	Túmulo#	CLEAR_NPC,3,3,{
	.@party_id = getcharid(1);
	if ('ins_baphomet == 0) {
		mes "O Túmulo está trêmulo...";
		next;
		mes "Ao tocá-lo, posso ouvir uma voz";
		next;
		specialeffect2 EF_CONE;
		mes "[Túmulo do Herói]";
		mes "Estava esperando que algum aventureiro forte como você voltasse...";
		next;
		while(1) {
			switch(select("Quem é você?:Esperando por mim?:Cancelar.")) {
			case 1:
				mes "[Túmulo do Herói]";
				mes "Eu sou um dos aventureiros que tentaram parar o Baphomet. Agora, estou morto e só minha alma que me resta...";
				next;
				mes "[Túmulo do Herói]";
				mes "Como sabe... Não temos tempo suficiente. Não podem parar o Baphomet sozinhos. Seu poder continua forte durante décadas.";
				next;
				mes "[Túmulo do Herói]";
				mes "No passado, meu grupo e eu selamos o Baphomet no segundo andar e bloqueamos a entrada.";
				next;
				mes "[Túmulo do Herói]";
				mes "Mudei a essência da minha alma, para que eu possa permanecer neste mundo. Isso foi quando eu me tornei guardião deste túmulo.";
				next;
				break;
			case 2:
				mes "[Túmulo do Herói]";
				mes "O poder de Baphomet pode quebrar o selo que fizemos. Se eu não reativá-los... Ele irá finalmente renascer.";
				next;
				mes "[Túmulo do Herói]";
				mes "Para poder entrar, você deve substancializar minha alma. Vou abrir a porta e reativar os selos enfraquecidos depois que estiver substancializado.";
				next;
				mes "[Túmulo do Herói]";
				mes "Para isso você deve achar o meu corpo que deve estar dentro de algum desses Túmulos.";
				next;
				mes "[Túmulo do Herói]";
				mes "Se o ^0000FFlíder do grupo^000000 trouxer meu pingente, minha alma será substancializada.";
				'ins_baphomet = 1;
				close;
			case 3:
				mes "I can feel the voice becoming faint.";
				close;
			}
		}
	}
	else if ('ins_baphomet == 1) {
		mes "[Túmulo do Herói]";
		mes "Para poder entrar, você deve substancializar minha alma. Vou abrir a porta e reativar os selos enfraquecidos depois que estiver substancializado.";
		next;
		mes "[Túmulo do Herói]";
		mes "Para substancializar minha alma, você deve encontrar meu pingente, que deve estar dentro de algum desses Túmulos.";
		next;
		mes "[Túmulo do Herói]";
		mes "Se o ^0000FFlíder do grupo^000000 trouxer meu pingente, minha alma será substancializada.";
		close;
	}
	else if (('ins_baphomet == 2) && (getpartyleader(.@party_id,2) == getcharid(0))) {
		mes "[Túmulo do Herói]";
		mes "Encontraram meu pingente?";
		next;
		if (countitem(6003) > 0) {
			specialeffect EF_MAPPILLAR;
			mes "[Túmulo do Herói]";
			mes "Sim... É esse... Meu Pingente...";
			next;
			delitem 6003,1; //Soul_Pendant
			enablenpc instance_npcname("Alma do Herói#1F");
			disablenpc instance_npcname("Túmulo#");
			mes "[Túmulo do Herói]";
			mes "Agora posso substancializar minha alma. Esperarei no altar de fogo no centro do mapa. Me encontre lá.";
			next;
			mapannounce instance_mapname("1@cata"), "Alma do Herói: Esperarei no altar de fogo no centro do mapa.",bc_map,"0xFFFF00";
			mes "Posso ouvir uma voz fraca.";
			close;
		}
		else {
			mes "[Túmulo do Herói]";
			mes "Ainda está aqui? Traga meu Pingente o mais rápido possível.";
			next;
			mes "[Túmulo do Herói]";
			mes "Você pode encontrar meu corpo com meu pingente nesse mapa.";
			close;
		}
	}
	else {
		mes "[Túmulo do Herói]";
		mes "Eu quero falar com o ^0000FFlíder do grupo^000000. Outros, aguardem aqui.";
		close;
	}
	end;

OnTouch:
	if ('ins_baphomet == 0) {
		mes "'Krrrr... Krrrr...'";
		next;
		mes "Posso sentir algo estranho no túmulo. É como se alguém estivesse pedindo algo em silêncio...";
		close;
	}
	end;

OnInstanceInit: // Temporary fix for @reloadscript.
	'ins_baphomet = 0;
	end;
}

1@cata,176,119,4	script	Alma do Herói#1F	4_M_CHAMPSOUL,{
	.@party_id = getcharid(1);
	cutin "ins_cata_champ_n",2;
	if ('ins_baphomet == 2) {
		mes "[Alma do Herói]";
		mes "Com sua ajuda, minha alma pode ser substancializada. Quero falar, mas não temos tempo suficiente...";
		next;
		mes "[Alma do Herói]";
		mes "Posso ficar assim por uma hora. Para chegar ao altar principal do subterrâneo, você deve me ajudar a realizar a cerimônia de abertura de cada selo.";
		next;
		mes "[Alma do Herói]";
		mes "Falarei o que você deve fazer. Primeiro, pegue as ^0000FFEssências de Fogo^000000 das tochas dos túmulos...";
		next;
		mes "[Alma do Herói]";
		mes "Segundo, todos os membros que entrarem no subterrâneo devem levar um símbolo que é chamado Símbolo do Apóstolo.";
		next;
		while(1) {
			cutin "ins_cata_champ_n",2;
			switch(select("Essências de Fogo?:Símbolo do Apóstolo?:O que fazer?:Estou pronto!")) {
			case 1:
				++.@ins_baphomet_1f_1;
				mes "[Alma do Herói]";
				mes "Você pode ver as Tochas por aqui. Elas contém a essência de Thor, o deus do Trovão. Elas são herdadas de nossos antecessores para parar a escuridão de Baphomet...";
				next;
				mes "[Alma do Herói]";
				mes "Para abrir o portão que leva ao subsolo é preciso ser purificado pelas ^0000FFEssências de Fogo^000000.";
				next;
				mes "[Alma do Herói]";
				mes "Uma coisa que você deve lembrar é que ... Estas ^0000FFEssências de Fogo^000000 só podem ser recolhidas pelo herdeiro da Fé.";
				next;
				mes "[Alma do Herói]";
				mes "Somente o líder do grupo que pode recolher as ^0000FFEssências de Fogo^000000.";
				next;
				break;
			case 2:
				++.@ins_baphomet_1f_2;
				mes "[Alma do Herói]";
				mes "Naquele tempo, era impossível se livrar do demônio Baphomet por nós mesmos. Depois de numerosos heróis terem sacrificado suas vidas, nós conseguimos selar o Demônio nessa abadia.";
				next;
				mes "[Alma do Herói]";
				mes "Entretando, Baphomet nunca cairá. Seu poder continua aumentando. Esse poder trouxe nova vida ao seu túmulo.";
				next;
				mes "[Alma do Herói]";
				mes "Alguns desses monstros possuem um grande poder. Baphomet os chamam de 'Apóstolos'.";
				next;
				mes "[Alma do Herói]";
				mes "Os Apóstolos se clonam para encher este túmulo de poder. Eles são os monstros de dentro dessas catacumbas.";
				next;
				mes "[Alma do Herói]";
				mes "Matem os Apóstolos. Somente os verdadeiros que derrubam o ^0000FFSímbolo do Apóstolo^000000...";
				next;
				mes "[Alma do Herói]";
				mes "Se cada membro do grupo possuir um ^0000FFSímbolo do Apóstolo^000000, Baphomet não será capaz de perceber sua entrada no Altar Principal subterrâneo.";
				next;
				mes "[Alma do Herói]";
				mes "Se Baphomet perceber você passando pelo portão selado, Ele pode liberar seu tremendo poder mágico. Então, esta abadia será demolida.";
				next;
				mes "[Alma do Herói]";
				mes "Mate os apóstolos e pegue o ^0000FFSímbolo do Apóstolo^000000.";
				next;
				break;
			case 3:
				++.@ins_baphomet_1f_3;
				if (getpartyleader(.@party_id,2) == getcharid(0)) {
					mes "[Alma do Herói]";
					mes "Você parece ser o líder do grupo. Você precisa ter ^0000FF10 Essências de Fogo^000000 das tochas.";
					next;
					mes "[Alma do Herói]";
					mes "Agora irei cravar o símbolo que mostra que você é o herdeiro da fé. Saiba que, somente você pode carregar as Essências do Fogo.";
					next;
					specialeffect2 EF_HOLYHIT;
					mes "[Alma do Herói]";
					mes "Você deve carregar o ^0000FFSímbolo do Apóstolo^000000 também. Então prepare-se.";
					next;
					mes "[Alma do Herói]";
					mes "Me avise quando todos estiverem prontos que irei abrir o portão selado.";
					next;
				}
				else {
					mes "[Alma do Herói]";
					mes "Esteja certo que o Líder do Grupo escutou minhas explicações para o andamento da instância";
					next;
					mes "[Alma do Herói]";
					mes "Matem os Apóstolos de Baphomet e peguem seu ^0000FFSímbolo do Apóstolo^000000.";
					next;
					mes "[Alma do Herói]";
					mes "É melhor matarem todos os Apóstolos pois é difícil saber qual é o certo.";
					next;
					mes "[Alma do Herói]";
					mes "Está pronto?";
					mes "Esteja certo que você escutou todas as minhas explicações antes de ir.";
					next;
				}
				break;
			case 4:
				if ((.@ins_baphomet_1f_1 > 0) && (.@ins_baphomet_1f_2 > 0) && (.@ins_baphomet_1f_3 > 0)) {
					cutin "ins_cata_champ_n",2;
					mes "[Alma do Herói]";
					mes "Está pronto? Abrirei o portão selado agora mesmo.";
					next;
					.@exitloop = 1;
				}
				else {
					cutin "ins_cata_champ_a",2;
					mes "[Alma do Herói]";
					mes "Parece ser difícil mas espero que sua bravura e suas habilidades sejam o suficiente para isso.";
					next;
					mes "[Alma do Herói]";
					mes "Lembre-se, eu preciso que você traga para mim ^FFAA0010 Essências de Fogo^000000 encontradas nas tochas desse lugar e o ^0000FFSímbolo do Apóstolo^000000 dos Apóstolos de Baphomet.";
					next;
				}
				break;
			}
			if (.@exitloop) break;
		}
		if (getpartyleader(.@party_id,2) == getcharid(0)) {
			mes "[Alma do Herói]";
			mes "Lembrando novamente, Devo ser substancializado dentro das próximas horas. Então devem finalizar o trabalho dentro desse tempo!";
			'ins_baphomet = 3;
			donpcevent instance_npcname("ins_baphomet_1f_timer")+"::OnEnable";
			donpcevent instance_npcname("ins_baphomet_lotto2")+"::OnEnable";
			donpcevent instance_npcname("ins_baphomet_lotto3")+"::OnEnable";
		}
		else {
			mes "[Alma do Herói]";
			mes "Lembrando novamente, Devo ser substancializado dentro das próximas horas. Então devem finalizar o trabalho dentro desse tempo!";
			//'ins_baphomet = 3;
		}
		close2;
		cutin "",255;
		end;
	}
	else if (('ins_baphomet == 3) && (getpartyleader(.@party_id,2) == getcharid(0))) {
		cutin "ins_cata_champ_n",2;
		mes "[Alma do Herói]";
		mes "Você pegou as 10^0000FFEssências de Fogo^000000 e o ^0000FFSímbolo do Apóstolo^000000?";
		next;
		if ((countitem(6001) > 9) && (countitem(6002) > 0)) {
			delitem 6001,countitem(6001); //Essence_Of_Fire
			'ins_baphomet = 4;
			mes "[Alma do Herói]";
			mes "Ok. Você fez seu trabalho. Agora cheque com seus companheiros se eles já terminaram.";
		}
		else {
			mes "[Alma do Herói]";
			mes "Não estão prontos ainda? eu preciso que você traga para mim ^FFAA0010 Essências de Fogo^000000 encontradas nas tochas desse lugar e o ^0000FFSímbolo do Apóstolo^000000 dos Apóstolos de Baphomet.";
		}
		close2;
		cutin "",255;
		end;
	}
	else if ('ins_baphomet == 3) {
		cutin "ins_cata_champ_n",2;
		mes "[Alma do Herói]";
		mes "Você tem o ^0000FFSímbolo do Apóstolo^000000?";
		next;
		if (countitem(6002) > 0) {
			'ins_baphomet = 4;
			mes "[Alma do Herói]";
			mes "Ok. Você fez seu trabalho. Agora cheque com seus companheiros se eles já terminaram.";
		}
		else {
			mes "[Alma do Herói]";
			mes "Ainda não? eu preciso do ^0000FFSímbolo do Apóstolo^000000 dos Apóstolos de Baphomet.";
		}
		close2;
		cutin "",255;
		end;
	}
	else if (('ins_baphomet == 4) && (getpartyleader(.@party_id,2) == getcharid(0))) {
		cutin "ins_cata_champ_n",2;
		mes "[Alma do Herói]";
		mes "Você está pronto? Abri o portão selado neste momento. Para passar pelo portão, você deve levar o ^0000FFSímbolo do Apóstolo^000000.";
		next;
		'ins_baphomet = 5;
		specialeffect EF_TELEPORTATION;
		enablenpc instance_npcname("ins_bapho_to_2f");
		mes "[Alma do Herói]";
		mes "Agora já pode ir ao altar principal. Ele está localizado no canto inferior direito deste andar.";
		next;
		mes "[Alma do Herói]";
		mes "A verdadeira batalha começa... Irei os serguir e encontrar alguma forma de ajudar.";
		next;
		mes "[Alma do Herói]";
		mes "Vão à frente, guerreiros.";
		cutin "",255;
		mapannounce instance_mapname("1@cata"), "Alma do Herói: Agora já pode ir ao portão do Altar Principal. Ele está localizado no Sudeste deste andar.",bc_map,"0xFFFF00";
		close;
	}
	else if ('ins_baphomet == 4) {
		cutin "ins_cata_champ_n",2;
		mes "[Alma do Herói]";
		mes "Você está pronto? Abri o portão selado neste momento. Para passar pelo portão, você deve levar o ^0000FFSímbolo do Apóstolo^000000.";
		next;
		mes "[Alma do Herói]";
		mes "Só irei completar a abertura do portão selado, quando seu representante me disser que você está pronto.";
		cutin "",255;
		close;
	}
	else if ('ins_baphomet == 5) {
		cutin "ins_cata_champ_n",2;
		mes "[Alma do Herói]";
		mes "O que está fazendo? A entrada para o Altar está aberta, vá e lute! A entrada se encontra no sudeste deste andar.";
		cutin "",255;
		close;
	}
	else {
		cutin "ins_cata_champ_n",2;
		mes "[Alma do Herói]";
		mes "Não tenho nada a falar com você...";
		cutin "",255;
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Alma do Herói#1F");
	end;
}

// To 2F Warp
//============================================================
1@cata,281,12,0	script	ins_bapho_to_2f	WARPNPC,1,1,{
OnTouch:
	if (countitem(6002) > 0) {
		delitem 6002,countitem(6002); //Token_Of_Apostle
		'ins_baphomet = 5;
		warp instance_mapname("2@cata"),80,144;
		end;
	}
	else {
		mes "[Alma do Herói]";
		mes "Cadê seu Símbolo do Apóstolo? Eu disse que precisava dele para passar pelo portão.";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("ins_bapho_to_2f");
	end;
}

// Túmulos
//============================================================
-	script	Túmulo#SS1	-1,{
	if ('ins_baphomet == 1) {
		getitem 6003,1; //Soul_Pendant
		'ins_baphomet = 2;
		mes "Um pequeno objeto está brilhando abaixo do antigo túmulo.";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "Acho que é o pingente...";
		close;
	}
	else {
		mes "Posso sentir a tristeza vinda desse túmulo.";
		close;
	}
}
1@cata,86,214,0	duplicate(Túmulo#SS1)	Túmulo#1F_1T	CLEAR_NPC
1@cata,200,209,0	duplicate(Túmulo#SS1)	Túmulo#1F_2T	CLEAR_NPC
1@cata,230,195,0	duplicate(Túmulo#SS1)	Túmulo#1F_3T	CLEAR_NPC
1@cata,118,182,0	duplicate(Túmulo#SS1)	Túmulo#1F_4T	CLEAR_NPC
1@cata,193,182,0	duplicate(Túmulo#SS1)	Túmulo#1F_5T	CLEAR_NPC
1@cata,253,156,0	duplicate(Túmulo#SS1)	Túmulo#1F_6T	CLEAR_NPC
1@cata,88,154,0	duplicate(Túmulo#SS1)	Túmulo#1F_7T	CLEAR_NPC
1@cata,127,116,0	duplicate(Túmulo#SS1)	Túmulo#1F_8T	CLEAR_NPC
1@cata,90,98,0	duplicate(Túmulo#SS1)	Túmulo#1F_9T	CLEAR_NPC
1@cata,188,84,0	duplicate(Túmulo#SS1)	Túmulo#1F_10T	CLEAR_NPC
1@cata,244,42,0	duplicate(Túmulo#SS1)	Túmulo#1F_11T	CLEAR_NPC
1@cata,127,32,0	duplicate(Túmulo#SS1)	Túmulo#1F_12T	CLEAR_NPC
1@cata,267,30,0	duplicate(Túmulo#SS1)	Túmulo#1F_13T	CLEAR_NPC

-	script	Túmulo#SS2	-1,{
	mes "Posso sentir a tristeza vinda desse túmulo.";
	close;
}
1@cata,86,214,0	duplicate(Túmulo#SS2)	Túmulo#1F_1F	CLEAR_NPC
1@cata,200,209,0	duplicate(Túmulo#SS2)	Túmulo#1F_2F	CLEAR_NPC
1@cata,230,195,0	duplicate(Túmulo#SS2)	Túmulo#1F_3F	CLEAR_NPC
1@cata,118,182,0	duplicate(Túmulo#SS2)	Túmulo#1F_4F	CLEAR_NPC
1@cata,193,182,0	duplicate(Túmulo#SS2)	Túmulo#1F_5F	CLEAR_NPC
1@cata,253,156,0	duplicate(Túmulo#SS2)	Túmulo#1F_6F	CLEAR_NPC
1@cata,88,154,0	duplicate(Túmulo#SS2)	Túmulo#1F_7F	CLEAR_NPC
1@cata,127,116,0	duplicate(Túmulo#SS2)	Túmulo#1F_8F	CLEAR_NPC
1@cata,90,98,0	duplicate(Túmulo#SS2)	Túmulo#1F_9F	CLEAR_NPC
1@cata,188,84,0	duplicate(Túmulo#SS2)	Túmulo#1F_10F	CLEAR_NPC
1@cata,244,42,0	duplicate(Túmulo#SS2)	Túmulo#1F_11F	CLEAR_NPC
1@cata,127,32,0	duplicate(Túmulo#SS2)	Túmulo#1F_12F	CLEAR_NPC
1@cata,267,30,0	duplicate(Túmulo#SS2)	Túmulo#1F_13F	CLEAR_NPC

// Tochas
//============================================================
-	script	Tocha#SS	-1,{
	.@party_id = getcharid(1);
	if (getpartyleader(.@party_id,2) == getcharid(0)) {
		if (('ins_baphomet == 3) && (countitem(6001) < 11)) {
			mes "Uma antiga tocha parece que pode queimar tudo que está a minha volta.";
			next;
			mes "Sua grande aparência e o calor de suas chamas me faz voltar... Porém tive coragem e estiquei o braço para segurá-la.";
			next;
			specialeffect2 EF_HOLYHIT;
			getitem 6001,1; //Essence_Of_Fire
			mes "O símbolo do herdeiro brilha. Nisso um pequeno cristal cai da tocha.";
			disablenpc instance_npcname(strnpcinfo(0));
			close;
		}
		else if (('ins_baphomet == 3) && (countitem(6001) > 10)) {
			mes "Você já tem 10 Essências de Fogo. Você não precisa pegar mais nenhuma.";
			close;
		}
		else {
			mes "Você não precisa mais pegar as Essências de Fogo.";
			close;
		}
	}
	else {
		mes "Uma antiga tocha parece que pode queimar tudo que está a minha volta.";
		next;
		mes "[Unknown Voice]";
		mes "Você não é o Herdeiro da Fé. Não coloque essas mãos imundas nas Essências do Fogo.";
		specialeffect2 EF_FIRESPLASHHIT;
		percentheal -50,0;
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}
1@cata,267,210,0	duplicate(Tocha#SS)	Tocha#1	CLEAR_NPC
1@cata,85,182,0	duplicate(Tocha#SS)	Tocha#2	CLEAR_NPC
1@cata,267,154,0	duplicate(Tocha#SS)	Tocha#3	CLEAR_NPC
1@cata,157,139,0	duplicate(Tocha#SS)	Tocha#4	CLEAR_NPC
1@cata,193,138,0	duplicate(Tocha#SS)	Tocha#5	CLEAR_NPC
1@cata,193,102,0	duplicate(Tocha#SS)	Tocha#6	CLEAR_NPC
1@cata,157,102,0	duplicate(Tocha#SS)	Tocha#7	CLEAR_NPC
1@cata,113,112,0	duplicate(Tocha#SS)	Tocha#8	CLEAR_NPC
1@cata,253,56,0	duplicate(Tocha#SS)	Tocha#9	CLEAR_NPC
1@cata,239,56,0	duplicate(Tocha#SS)	Tocha#10	CLEAR_NPC
1@cata,71,42,0	duplicate(Tocha#SS)	Tocha#11	CLEAR_NPC
1@cata,155,14,0	duplicate(Tocha#SS)	Tocha#12	CLEAR_NPC

1@cata,1,1,0	script	ins_baphomet_1f_timer	-1,{
OnInstanceInit:
	disablenpc instance_npcname("ins_baphomet_1f_timer");
	end;

OnEnable:
	enablenpc instance_npcname("ins_baphomet_1f_timer");
	initnpctimer;
	end;

OnDisable:
	disablenpc instance_npcname("ins_baphomet_1f_timer");
	stopnpctimer;
	end;

OnTimer1800000:
	mapannounce instance_mapname("1@cata"), "Alma do Herói: Não temos tempo suficiente. Apressem-se!",bc_map,"0xFFFF00";
	end;

OnTimer2400000:
	mapannounce instance_mapname("1@cata"), "Alma do Herói: Meu corpo está desaparecendo... Apressem-se!",bc_map,"0xFFFF00";
	end;

OnTimer3000000:
	mapannounce instance_mapname("1@cata"), "Alma do Herói: Everything is over... There is no other way but to wait for the next chance...",bc_map,"0xFFFF00";
	end;

OnTimer3050000:
	mapannounce instance_mapname("1@cata"), "Alma do Herói: Nós falhamos... Contudo... Ainda temos uma chance. Espero que treinem mais até a hora chegar.",bc_map,"0xFFFF00";
	end;

OnTimer3100000:
	mapannounce instance_mapname("1@cata"), "Você falhou em abrir o selo no Altar Principal.",bc_map,"0xFFFF00";
	end;

OnTimer3500000:
	instance_warpall "monk_test",310,150;
	stopnpctimer;
	end;
}

2@cata,80,144,0	script	ins_2f_enter	-1,3,3,{
OnTouch:
	donpcevent instance_npcname("ins_2f_enter_broad")+"::OnEnable";
	disablenpc instance_npcname("ins_2f_enter");
	end;
}

2@cata,1,1,0	script	ins_2f_enter_broad	-1,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("ins_2f_enter_broad");
	end;

OnEnable:
	enablenpc instance_npcname("ins_2f_enter_broad");
	initnpctimer;
	end;

OnTimer10000:
	mapannounce instance_mapname("2@cata"), "Baphomet: Humanos... interferindo novamente...",bc_map,"0xdb7093";
	end;

OnTimer13000:
	mapannounce instance_mapname("2@cata"), "Apóstolo de Baphomet: Humanos! Humanos invadiram nosso santuário!",bc_map,"0xFFFF00";
	end;

OnTimer16000:
	mapannounce instance_mapname("2@cata"), "Apóstolo de Baphomet: Matem-nos! Não deixem parar a ressurreição de nosso mestre!",bc_map,"0xFFFF00";
	end;

OnTimer18000:
	mapannounce instance_mapname("2@cata"), "Apóstolo de Baphomet: Apressem-se em liberar os selos dos altares! O retorno de nosso mestre está sobre nós.",bc_map,"0xFFFF00";
	stopnpctimer;
	disablenpc instance_npcname("ins_2f_enter_broad");
	end;
}

2@cata,50,67,0	script	slave_left	-1,5,5,{
OnTouch:
	.@map$ = instance_mapname("2@cata");
	mapannounce .@map$, "Apóstolo de Baphomet: Matem os Humanos! Não os deixem interromper o renascimento de nosso Mestre!",bc_map,"0xFFFF00";
	monster .@map$,55,67,"Apóstolo de Baphomet",1869,1;
	monster .@map$,51,67,"Apóstolo de Baphomet",1291,1;
	monster .@map$,58,67,"Apóstolo de Baphomet",1292,1;
	monster .@map$,53,67,"Apóstolo de Baphomet",1291,1;
	monster .@map$,54,67,"Apóstolo de Baphomet",1869,1;
	monster .@map$,55,67,"Apóstolo de Baphomet",1291,1;
	monster .@map$,56,67,"Apóstolo de Baphomet",1117,1;
	monster .@map$,58,66,"Apóstolo de Baphomet",1869,1;
	monster .@map$,56,66,"Apóstolo de Baphomet",1117,1;
	monster .@map$,60,66,"Apóstolo de Baphomet",1132,1;
	monster .@map$,59,66,"Apóstolo de Baphomet",1117,1;
	monster .@map$,54,66,"Apóstolo de Baphomet",1132,1;
	monster .@map$,55,66,"Apóstolo de Baphomet",1292,1;
	monster .@map$,56,66,"Apóstolo de Baphomet",1132,1;
	monster .@map$,50,65,"Apóstolo de Baphomet",1867,1;
	monster .@map$,61,65,"Apóstolo de Baphomet",1292,1;
	disablenpc instance_npcname("slave_left");
	end;
}

2@cata,109,67,0	script	slave_right	-1,5,5,{
OnTouch:
	.@map$ = instance_mapname("2@cata");
	mapannounce .@map$, "Apóstolo de Baphomet: Matem os Humanos! Não os deixem interromper o renascimento de nosso Mestre!",bc_map,"0xFFFF00";
	monster .@map$,105,67,"Apóstolo de Baphomet",1869,1;
	monster .@map$,104,67,"Apóstolo de Baphomet",1291,1;
	monster .@map$,107,67,"Apóstolo de Baphomet",1869,1;
	monster .@map$,106,67,"Apóstolo de Baphomet",1291,1;
	monster .@map$,102,67,"Apóstolo de Baphomet",1869,1;
	monster .@map$,103,67,"Apóstolo de Baphomet",1291,1;
	monster .@map$,103,67,"Apóstolo de Baphomet",1117,1;
	monster .@map$,109,66,"Apóstolo de Baphomet",1117,1;
	monster .@map$,108,66,"Apóstolo de Baphomet",1132,1;
	monster .@map$,101,66,"Apóstolo de Baphomet",1117,1;
	monster .@map$,106,66,"Apóstolo de Baphomet",1292,1;
	monster .@map$,102,66,"Apóstolo de Baphomet",1132,1;
	monster .@map$,104,66,"Apóstolo de Baphomet",1292,1;
	monster .@map$,103,66,"Apóstolo de Baphomet",1132,1;
	monster .@map$,109,65,"Apóstolo de Baphomet",1867,1;
	monster .@map$,108,65,"Apóstolo de Baphomet",1292,1;
	disablenpc instance_npcname("slave_right");
	end;
}

2@cata,79,39,0	script	slave_down	-1,5,5,{
OnTouch:
	.@map$ = instance_mapname("2@cata");
	mapannounce .@map$, "Apóstolo de Baphomet: Matem os Humanos! Não os deixem interromper o renascimento de nosso Mestre!",bc_map,"0xFFFF00";
	monster .@map$,78,41,"Apóstolo de Baphomet",1869,1;
	monster .@map$,79,42,"Apóstolo de Baphomet",1291,1;
	monster .@map$,78,46,"Apóstolo de Baphomet",1869,1;
	monster .@map$,81,41,"Apóstolo de Baphomet",1291,1;
	monster .@map$,81,42,"Apóstolo de Baphomet",1869,1;
	monster .@map$,79,43,"Apóstolo de Baphomet",1291,1;
	monster .@map$,77,40,"Apóstolo de Baphomet",1117,1;
	monster .@map$,79,41,"Apóstolo de Baphomet",1132,1;
	monster .@map$,79,42,"Apóstolo de Baphomet",1117,1;
	monster .@map$,79,43,"Apóstolo de Baphomet",1132,1;
	monster .@map$,79,48,"Apóstolo de Baphomet",1117,1;
	monster .@map$,78,49,"Apóstolo de Baphomet",1132,1;
	monster .@map$,78,41,"Apóstolo de Baphomet",1292,1;
	monster .@map$,74,42,"Apóstolo de Baphomet",1292,1;
	monster .@map$,72,48,"Apóstolo de Baphomet",1867,1;
	monster .@map$,72,38,"Apóstolo de Baphomet",1292,1;
	disablenpc instance_npcname("slave_down");
	end;
}

// Selo Mágico
//============================================================
-	script	Selo Mágico#SS	-1,{
	.@seal_check = questprogress(3041,PLAYTIME);
	if (.@seal_check == 1) {
		specialeffect2 EF_SILENCEATTACK;
		percentheal -50,0;
		sc_start Eff_Stone,30000,0;
		mes "Seu SP não esta cheio ainda. Você perdeu seu SP no altar, mas parece que o poder do selo ainda não voltou.";
		close;
	}
	if (.@seal_check == 2) erasequest 3041;
	specialeffect EF_LEXDIVINA;
	disablenpc instance_npcname(strnpcinfo(0));
	.@map$ = instance_mapname("2@cata");
	if (strnpcinfo(2) == "0") areamobuseskill .@map$,79,81,10,1929,"NPC_INVINCIBLEOFF",1,0,0,e_hlp,0;
	else if (strnpcinfo(2) == "2") areamobuseskill .@map$,123,109,10,1929,"NPC_INVINCIBLEOFF",1,0,0,e_hlp,0;
	else if (strnpcinfo(2) == "4") areamobuseskill .@map$,123,22,10,1929,"NPC_INVINCIBLEOFF",1,0,0,e_hlp,0;
	else if (strnpcinfo(2) == "8") areamobuseskill .@map$,35,21,10,1929,"NPC_INVINCIBLEOFF",1,0,0,e_hlp,0;
	else if (strnpcinfo(2) == "10") areamobuseskill .@map$,35,109,10,1929,"NPC_INVINCIBLEOFF",1,0,0,e_hlp,0;
	percentheal -50,0;
	sc_start Eff_Stone,20000,0;
	setquest 3041;
	mapannounce .@map$, "O selo é ativado colocando o poder mágico sobre o altar.",bc_map,"0x87ceeb";
	mes "Eu posso sentir o poder voltando ao altar colocando seu poder mágico.";
	next;
	mes "Você não pode colocar o selo novamente durante 3 minutos, pois perdeu seu SP com isso.";
	close;

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}
2@cata,79,81,0	duplicate(Selo Mágico#SS)	Selo Mágico#0	CLEAR_NPC
2@cata,123,109,0	duplicate(Selo Mágico#SS)	Selo Mágico#2	CLEAR_NPC
2@cata,123,22,0	duplicate(Selo Mágico#SS)	Selo Mágico#4	CLEAR_NPC
2@cata,35,21,0	duplicate(Selo Mágico#SS)	Selo Mágico#8	CLEAR_NPC
2@cata,35,109,0	duplicate(Selo Mágico#SS)	Selo Mágico#10	CLEAR_NPC

2@cata,79,65,0	script	Altar Principal#ss	CLEAR_NPC,{
	.@party_id = getcharid(1);
	if (('ins_baphomet == 5) && (getpartyleader(.@party_id,2) == getcharid(0))) {
		mes "Um poder malígno, terrível demais para descrever, passa pelo altar irradiando uma cor violeta.";
		next;
		mes "Pergaminhos Mágicos piscam rapidamente, na tentativa de suprimir esse poder terrível.";
		next;
		mes "O fundo do Altar Principal treme furiosamente.";
		next;
		specialeffect EF_METEORSTORM;
		specialeffect EF_METEORSTORM;
		mes "[" + strcharinfo(0) + "]";
		mes "Olhem! Algo... Algo está vindo!";
		'ins_baphomet = 6;
		donpcevent instance_npcname("ins_2f_hero_broad")+"::OnEnable";
		disablenpc instance_npcname("Altar Principal#ss");
		close;
	}
	else {
		mes "Um poder malígno, terrível demais para descrever, passa pelo altar irradiando uma cor violeta.";
		next;
		mes "Pergaminhos Mágicos piscam rapidamente, na tentativa de suprimir esse poder terrível.";
		close;
	}
	end;
}

2@cata,80,63,4	script	Alma do Herói#2F	4_M_CHAMPSOUL,{
	cutin "ins_cata_champ_s",2;
	if (questprogress(3041)) erasequest 3041;
	mes "[Alma do Herói]";
	mes "Bom trablaho, meus descendentes... Vocês realizaram meu desejo que muitos não cumprem faz tempo.";
	next;
	mes "[Alma do Herói]";
	mes "Realmente aprecio sua ajuda.";
	mes "Suas almas finalmente podem descansar em paz...";
	next;
	mes "[Alma do Herói]";
	mes "A luta pela paz neste mundo nunca vai acabar. Mas ... o meu papel aqui finalmente chega ao fim, porque há bravos heróis como você.";
	next;
	switch(select("Leve-me ao Santuário:Terminar conversa.")) {
	case 1:
		mes "[Alma do Herói]";
		mes "Ok. Tirarei seu grupo daqui com segurança.";
		next;
		mes "[Alma do Herói]";
		mes "Quando sair, diga um oi ao Patrick.";
		cutin "",255;
		'ins_baphomet = 0;
		warp "monk_test",310,150;
		close;
	case 2:
		mes "[Alma do Herói]";
		mes "Ainda tem algo para fazer aqui? Quando estiver pronto para sair, fale comigo...";
		cutin "",255;
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Alma do Herói#2F");
	end;
}

2@cata,1,2,0	script	ins_2f_hero_broad	-1,{
OnEnable:
	enablenpc instance_npcname("ins_2f_hero_broad");
	initnpctimer;
	end;

OnDisable:
	disablenpc instance_npcname("ins_2f_hero_broad");
	end;

OnTimer3000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Meu deus! O selo do Altar Principal está enfraquecendo.",bc_map,"0xFFFF00";
	end;

OnTimer6000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Meus decendentes... Escutem com atenção o que irei dizer.",bc_map,"0xFFFF00";
	end;

OnTimer9000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Os altares que controlam o poder do Altar Principal estão localizados no Nordeste, Sudeste, Sudoeste e Noroeste desta sala.",bc_map,"0xFFFF00";
	end;

OnTimer12000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Encontre estes altares e ative os seus selos antes que Baphomet reviva.",bc_map,"0xFFFF00";
	end;

OnTimer15000:
	mapannounce instance_mapname("2@cata"), "Baphomet: Tarde demais, fracos... Agora, vocês irão sentir o desespero da morte!",bc_map,"0xdb7093";
	end;

OnTimer17000:
	mapannounce instance_mapname("2@cata"), "Baphomet: Ninguém pode me prejudicar aqui. Você será o meu primeiro sacrifício.",bc_map,"0xdb7093";
	donpcevent instance_npcname("control_baphomet")+"::OnEnable";
	donpcevent instance_npcname("ins_2f_hero_broad2")+"::OnEnable";
	stopnpctimer;
	disablenpc instance_npcname("ins_2f_hero_broad");
	end;
}

2@cata,3,3,0	script	control_baphomet	-1,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("control_baphomet");
	end;

OnEnable:
	enablenpc instance_npcname("control_baphomet");
	donpcevent instance_npcname("2f_callmon_pattern_c")+"::OnEnable";
	monster instance_mapname("2@cata"),79,64,"Baphomet#",1929,1,instance_npcname("control_baphomet")+"::OnMyMobDead";
	end;

OnMyMobDead:
	.@map$ = instance_mapname("2@cata");
	if (mobcount(.@map$,instance_npcname("control_baphomet")+"::OnMyMobDead") < 1) {
		'ins_baphomet = 7;
		erasequest 3041;
		mapannounce .@map$, "Baphomet: Não! Nãoooo!Levarei isso como insulto, seus insetos!... Não...",bc_map,"0xdb7093";
		enablenpc instance_npcname("Alma do Herói#2F");
		disablenpc instance_npcname("slave_down");
		disablenpc instance_npcname("slave_left");
		disablenpc instance_npcname("slave_right");
		donpcevent instance_npcname("ins_2f_hero_broad")+"::OnDisable";
		donpcevent instance_npcname("ins_2f_hero_broad2")+"::OnDisable";
		donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::OnDisable";
		donpcevent instance_npcname("2f_callmon_pattern_c")+"::OnDisable";
		donpcevent instance_npcname("2f_callmon_pattern")+"::OnDisable";
		donpcevent instance_npcname("ins_2f_hero_pattern")+"::OnDisable";
		donpcevent instance_npcname("ins_2f_enter_broad")+"::OnDisable";
		donpcevent instance_npcname("control_baphomet")+"::OnDisable";
		disablenpc instance_npcname("control_baphomet");
	}
	end;
}

2@cata,2,2,0	script	ins_2f_hero_broad2	-1,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("ins_2f_hero_broad2");
	end;

OnEnable:
	enablenpc instance_npcname("ins_2f_hero_broad2");
	initnpctimer;
	end;

OnTimer8000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Não se preocupem, o Baphomet ainda pode ser derrotado!",bc_map,"0xFFFF00";
	end;

OnTimer11000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Vá para os alteres e ativem os selos.",bc_map,"0xFFFF00";
	end;

OnTimer13000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Quando os selos se recuperarem, Baphomet ficará vulnerável.",bc_map,"0xFFFF00";
	end;

OnTimer16000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Você deve atrair Baphomet aos altares não selados. Caso contrário, seu esforço será inútil.",bc_map,"0xFFFF00";
	end;

OnTimer19000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Temos apenas 1 hora para parar Baphomet. Se o tempo acabar, o poder dos selos será inútil.",bc_map,"0xFFFF00";
	end;

OnTimer22000:
	mapannounce instance_mapname("2@cata"), "Baphomet: É inútil. Faça mais selos. Vou esmagá-los. Nenhum de vocês sobreviverão!",bc_map,"0xdb7093";
	end;

OnTimer26000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: O poder mágico do selo central está se esgotando. Vá para o selo central e coloque o poder mágico.",bc_map,"0xFFFF00";
	enablenpc instance_npcname("Selo Mágico#0");
	disablenpc instance_npcname("Selo Mágico#2");
	disablenpc instance_npcname("Selo Mágico#4");
	disablenpc instance_npcname("Selo Mágico#8");
	disablenpc instance_npcname("Selo Mágico#10");
	donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::OnEnable";
	stopnpctimer;
	disablenpc instance_npcname("ins_2f_hero_broad2");
	end;
}

2@cata,4,1,0	script	2f_callmon_pattern_c	-1,{
OnInstanceInit:
	disablenpc instance_npcname("2f_callmon_pattern_c");
	end;

OnEnable:
	enablenpc instance_npcname("2f_callmon_pattern_c");
	initnpctimer;
	donpcevent instance_npcname("2f_callmon_pattern_c")+"::OnGo";
	end;

OnDisable:
	stopnpctimer;
	disablenpc instance_npcname("2f_callmon_pattern_c");
	end;

OnGo:
	donpcevent instance_npcname("2f_callmon_pattern")+"::OnEnable";
	end;

OnTimer3600000:
	donpcevent instance_npcname("2f_callmon_pattern_c")+"::OnDisable";
	end;
}

2@cata,4,2,0	script	2f_callmon_pattern	-1,{
OnInstanceInit:
	disablenpc instance_npcname("2f_callmon_pattern");
	end;

OnEnable:
	enablenpc instance_npcname("2f_callmon_pattern");
	stopnpctimer;
	initnpctimer;
	end;

OnDisable:
	disablenpc instance_npcname("2f_callmon_pattern");
	stopnpctimer;
	end;

OnTimer300000:
	enablenpc instance_npcname("slave_down");
	enablenpc instance_npcname("slave_left");
	enablenpc instance_npcname("slave_right");
	donpcevent instance_npcname("2f_callmon_pattern_c")+"::OnGo";
	end;
}

2@cata,3,1,0	script	ins_2f_hero_pattern_c	-1,{
OnInstanceInit:
	disablenpc instance_npcname("ins_2f_hero_pattern_c");
	end;

OnEnable:
	enablenpc instance_npcname("ins_2f_hero_pattern_c");
	initnpctimer;
	donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::OnGo";
	end;

OnGo:
	donpcevent instance_npcname("ins_2f_hero_pattern")+"::OnEnable";
	end;

OnDisable:
	stopnpctimer;
	disablenpc instance_npcname("Selo Mágico#0");
	disablenpc instance_npcname("Selo Mágico#2");
	disablenpc instance_npcname("Selo Mágico#4");
	disablenpc instance_npcname("Selo Mágico#8");
	disablenpc instance_npcname("Selo Mágico#10");
	donpcevent instance_npcname("ins_2f_hero_pattern")+"::OnDisable";
	disablenpc instance_npcname("ins_2f_hero_pattern_c");
	end;

OnTimer3600000:
	mapannounce instance_mapname("2@cata"), "Baphomet: krrrr... Agora você não pode me parar por causa de um selo. Basta esperar pela sua morte!",bc_map,"0xdb7093";
	end;

OnTimer3605000:
	mapannounce instance_mapname("2@cata"), "Alma do Herói: Você não pode mais parar o Baphomet com os selos. Tudo agora depende de deus ...",bc_map,"0xFFFF00";
	donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::OnDisable";
	end;
}

2@cata,3,2,0	script	ins_2f_hero_pattern	-1,{
OnInstanceInit:
	disablenpc instance_npcname("ins_2f_hero_pattern");
	end;

OnEnable:
	enablenpc instance_npcname("ins_2f_hero_pattern");
	initnpctimer;
	end;

OnDisable:
	disablenpc instance_npcname("ins_2f_hero_pattern");
	stopnpctimer;
	end;

OnTimer70000:
	switch(rand(1,5)) {
	case 1:
		mapannounce instance_mapname("2@cata"), "Alma do Herói: O selo do Altar Principal está enfraquecendo. Fortaleça o selo do Altar Principal!",bc_map,"0xFFFF00";
		enablenpc instance_npcname("Selo Mágico#0");
		disablenpc instance_npcname("Selo Mágico#2");
		disablenpc instance_npcname("Selo Mágico#4");
		disablenpc instance_npcname("Selo Mágico#8");
		disablenpc instance_npcname("Selo Mágico#10");
		break;
	case 2:
		mapannounce instance_mapname("2@cata"), "Alma do Herói: O poder do altar das 2 horas está enfraquecendo. Vá para o altar das 2 horas e reative-o.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("Selo Mágico#0");
		enablenpc instance_npcname("Selo Mágico#2");
		disablenpc instance_npcname("Selo Mágico#4");
		disablenpc instance_npcname("Selo Mágico#8");
		disablenpc instance_npcname("Selo Mágico#10");
		break;
	case 3:
		mapannounce instance_mapname("2@cata"), "Alma do Herói: O poder do altar das 4 horas está enfraquecendo. Vá para o altar das 4 horas e reative-o.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("Selo Mágico#0");
		disablenpc instance_npcname("Selo Mágico#2");
		enablenpc instance_npcname("Selo Mágico#4");
		disablenpc instance_npcname("Selo Mágico#8");
		disablenpc instance_npcname("Selo Mágico#10");
		break;
	case 4:
		mapannounce instance_mapname("2@cata"), "Alma do Herói: O poder do altar das 8 horas está enfraquecendo. Vá para o altar das 8 horas e reative-o.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("Selo Mágico#0");
		disablenpc instance_npcname("Selo Mágico#2");
		disablenpc instance_npcname("Selo Mágico#4");
		enablenpc instance_npcname("Selo Mágico#8");
		disablenpc instance_npcname("Selo Mágico#10");
		break;
	case 5:
		mapannounce instance_mapname("2@cata"), "Alma do Herói: O poder do altar das 10 horas está enfraquecendo. Vá para o altar das 10 horas e reative-o.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("Selo Mágico#0");
		disablenpc instance_npcname("Selo Mágico#2");
		disablenpc instance_npcname("Selo Mágico#4");
		disablenpc instance_npcname("Selo Mágico#8");
		enablenpc instance_npcname("Selo Mágico#10");
	}
	stopnpctimer;
	donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::OnGo";
	end;
}

1@cata,1,1,0	script	ins_1f_spawn_mobs	-1,{
OnInstanceInit:
	.@map$ = instance_mapname("1@cata");
	monster .@map$,0,0,"Zumbi Mestre",1298,10;
	monster .@map$,0,0,"Aparição",1291,10;
	//monster .@map$,0,0,"Mímico Antigo",1699,10;
	monster .@map$,0,0,"Crânio Flamejante",1869,10;
	monster .@map$,0,0,"Esqueleto General",1290,10;
	monster .@map$,0,0,"Zumbi Mestre",1298,10;
	monster .@map$,0,0,"Esqueleto General",1290,10;
	monster .@map$,0,0,"Crânio Flamejante",1869,10;
	monster .@map$,0,0,"Aparição",1291,10;
	monster .@map$,0,0,"Aparição",1291,10;
	monster .@map$,0,0,"Khalitzburg",1132,10;
	monster .@map$,0,0,"Khalitzburg",1132,10;
	monster .@map$,0,0,"Crânio Flamejante",1869,10;
	monster .@map$,0,0,"Crânio Flamejante",1869,10;
	monster .@map$,0,0,"Mímico Antigo",1699,10;
	monster .@map$,0,0,"Zumbi Mestre",1298,10;
	monster .@map$,0,0,"Mímico Antigo",1699,10;
	monster .@map$,0,0,"Zumbi Mestre",1298,10;
	monster .@map$,0,0,"Aparição",1291,10;
	monster .@map$,0,0,"Esqueleto General",1290,10;
	monster .@map$,0,0,"Esqueleto General",1290,10;
	monster .@map$,0,0,"Xamã do Vento",1263,10;
	monster .@map$,0,0,"Xamã do Vento",1263,10;
	monster .@map$,0,0,"Xamã do Vento",1263,10;
	monster .@map$,0,0,"Lude",1509,10;
	monster .@map$,0,0,"Lude",1509,10;
	monster .@map$,0,0,"Druida Malígno",1117,10;
	monster .@map$,0,0,"Druida Malígno",1117,10;
	monster .@map$,0,0,"Druida Malígno",1117,10;
	monster .@map$,0,0,"Banshee",1867,10;
	monster .@map$,0,0,"Ilusão das Trevas",1302,1;
	disablenpc instance_npcname("ins_1f_spawn_mobs");
	end;
}
